Rough Candy Hawk

Medium

# Incorrect tier updates when adding users

### Summary

When new users stake in to the pool, certain staking position may not be automatically promoted to the above tier.

As a result, affected position will be applied lower APY than expected. This will cause reward loss from that user.

### Root Cause

[File: edgen-staking/src/stake/LayerEdgeStaking.sol](https://github.com/sherlock-audit/2025-05-layeredge/blob/708c5b5345ed50cd0bde7b0a8a548c8936639683/edgen-staking/src/stake/LayerEdgeStaking.sol#L901-L906)
```solidity
901:             // Handle case where Tier 2 count stays the same
902:             else if (isRemoval) {
903:                 _findAndRecordTierChange(new_t1 + new_t2, n);
904:             } else if (!isRemoval) {
905:@>               _findAndRecordTierChange(old_t1 + old_t2, n);
906:             }
```

When tier2 count remains unchanged and the operation is addition, the code only updates the position with rank `old_t1 + old_t2`

But the following example shows this might be incorrect in some cases:

- Consider a scenario when a new user is added to the staking pool with 14 existing users
  - `n = 15`
  - `oldN = 14`
  - `isRemoval = false`
- 14 users will be split to [2, 4, 8]
- 15 users will be split to [3, 4, 8]
- So we have the following vars:
  - `old_t1` = 2
  - `old_t2` = 4
  - `new_t1` = 3
  - `new_t2 = 4`
- Notice the User 7. They need to be promoted from Tier3 to Tier2
- However since L905 only updates User 6, User 7 won't be promoted to Tier2


### Internal Pre-conditions

1. There are 14 stakers

### External Pre-conditions

N/A

### Attack Path

1. 15th user joins the pool
2. 7th user is not promoted to Tier 2

### Impact

User ranks are incorrectly updated. This will lead to reward loss from users.

### PoC

```solidity
pragma solidity ^0.8.20;

import "forge-std/Test.sol";
import {LayerEdgeStaking} from "@src/stake/LayerEdgeStaking.sol";
import {LayerEdgeToken} from "@test/mock/LayerEdgeToken.sol";
import {IERC20} from "openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

uint256 constant USER_SIZE = 100;
uint256 constant MIN_STAKE = 3000 * 1e18;

contract LayerEdgeStakingHarness is Test {
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;

    address public admin;

    uint256 public constant PRECISION = 1e18;

    constructor(LayerEdgeStaking _staking, LayerEdgeToken _token) {
        staking = _staking;
        token = _token;
    }

    function join(address userAddr) public {
        vm.startPrank(userAddr);
        deal(address(token), userAddr, MIN_STAKE);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);
        vm.stopPrank();
    }
}

contract POC is Test {
    LayerEdgeStakingHarness harness;
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;

    function setUp() external {
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();

        token = LayerEdgeToken(config.stakingToken);
        address admin = config.owner;
        vm.label(admin, "admin");
        vm.startPrank(admin);
        staking.setCompoundingStatus(true);
        staking.setMinStakeAmount(MIN_STAKE);
        vm.stopPrank();

        harness = new LayerEdgeStakingHarness(staking, token);
        targetContract(address(harness));
    }

    function testInvalidRank() public {
        // 15 users join the pool
        for (uint256 i = 1; i <= 15; i++) {
            address userAddr = address(uint160(1000 + i));
            harness.join(userAddr);
        }
        // notice the 7th user
        address userAddr = address(uint160(1007));
        LayerEdgeStaking.Tier tier = staking.getCurrentTier(userAddr);
        // According to current rank, user should be on Tier 2
        assertEq(uint256(tier), uint256(LayerEdgeStaking.Tier.Tier2));

        skip(365 days);
        uint256 accruedInterest = staking.calculateUnclaimedInterest(userAddr);
        // However, actual APY is 20% - Tier 3
        assertEq(accruedInterest, MIN_STAKE * 20 / 100);
    }
}

```

### Mitigation

The following diff can fix the issue:

```diff
diff --git a/edgen-staking/src/stake/LayerEdgeStaking.sol b/edgen-staking/src/stake/LayerEdgeStaking.sol
index a929afd..c2260a2 100644
--- a/edgen-staking/src/stake/LayerEdgeStaking.sol
+++ b/edgen-staking/src/stake/LayerEdgeStaking.sol
@@ -892,9 +892,9 @@ contract LayerEdgeStaking is
             }
             // Handle case where Tier 2 count stays the same
             else if (isRemoval) {
-                _findAndRecordTierChange(new_t1 + new_t2, n);
-            } else if (!isRemoval) {
                 _findAndRecordTierChange(old_t1 + old_t2, n);
+            } else if (!isRemoval) {
+                _findAndRecordTierChange(new_t1 + new_t2, n);
             }
         }
     }
```