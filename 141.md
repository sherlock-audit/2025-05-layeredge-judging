Soaring Yellow Squid

High

# incorrect stakerTierHistory calculation when isRemoval is false

### Summary

User will receive wrong amount of interest because of an error in stakerTierHistory calculation

### Root Cause

The bug is in line 897 of LayerEdgeStaking.sol:
https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L897 

This line should instead be replaced with line 895:
https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L895

### Internal Pre-conditions

- user calls `stake`
- in `_checkBoundariesAndRecord`: 
    - `new_t1 + new_t2` is greater than `0`
    - `new_t1` is greater than `old_t1`
    - `new_t2` is equal to `old_t2`
    - `isRemoval` is `false`

### External Pre-conditions

None.

### Attack Path

There are many instances where this bug can arise, but the simplest case is when going from 14 in tree stakers to 15.

This is because with 14 stakers we have:
- tier 1: 2 stakers
- tier 2: 4 stakers

While with 15 we have:
- tier 1: 3 stakers
- tier 2: 4 stakers

So in the function `_checkBoundariesAndRecord`:
- `new_t1 + new_t2` will be `> 0`
- `new_t1` will be greater than `old_t1`
- `new_t2` wil be equal to `old_t2`
- `isRemoval` will be `false`

This will cause the user ranked 7th to be recorded in history as Tier3 while he/she should be in Tier2.

### Impact

Some users will be incorrectly assigned in `stakerTierHistory` to Tier3 while they should have been in Tier2. This will result in them receiving less interest than they should have when calling `claimInterest`.

### PoC

- Create a new `.t.sol` file in `/edgen-staking/test/stake`.
- Paste the following code.
- Run `forge test`

The test checks whether `user7` is record in history as Tier3 but is calculated as in Tier2 by `getCurrentTier`.
The test succeeding means that this is true and so that's a bug. 

```solidity
pragma solidity ^0.8.20;

import {Test, console2} from "forge-std/Test.sol";
import {LayerEdgeStaking} from "@src/stake/LayerEdgeStaking.sol";
import {IERC20} from "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import {ERC1967Proxy} from "@openzeppelin/contracts/proxy/ERC1967/ERC1967Proxy.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";
import {LayerEdgeToken} from "@test/mock/LayerEdgeToken.sol";

contract TierBoundaryAndInterestTest is Test {
    LayerEdgeStaking public implementation;
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;

    // Users
    address public admin = makeAddr("admin");
    address public user1 = makeAddr("user1");
    address public user2 = makeAddr("user2");
    address public user3 = makeAddr("user3");
    address public user4 = makeAddr("user4");
    address public user5 = makeAddr("user5");
    address public user6 = makeAddr("user6");
    address public user7 = makeAddr("user7");
    address public user8 = makeAddr("user8");
    address public user9 = makeAddr("user9");
    address public user10 = makeAddr("user10");
    address public user11 = makeAddr("user11");
    address public user12 = makeAddr("user12");
    address public user13 = makeAddr("user13");
    address public user14 = makeAddr("user14");
    address public user15 = makeAddr("user15");
    address public user16 = makeAddr("user16");

    // Constants
    uint256 public constant PRECISION = 1e18;
    uint256 public constant MIN_STAKE = 3000 * 1e18;
    uint256 public constant LARGE_STAKE = 10000 * 1e18;
    uint256 public constant INITIAL_SUPPLY = 1_000_000 * 1e18;
    uint256 public constant REWARDS_AMOUNT = 100_000 * 1e18;

    function setUp() public {
        // Deploy token
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();

        token = LayerEdgeToken(config.stakingToken);
        admin = config.owner;

        // Fund admin and deposit rewards
        vm.startPrank(admin);
        token.approve(address(staking), REWARDS_AMOUNT);
        staking.depositRewards(REWARDS_AMOUNT);
        vm.stopPrank();

        // Fund users
        dealToken(user1, LARGE_STAKE);
        dealToken(user2, LARGE_STAKE);
        dealToken(user3, LARGE_STAKE);
        dealToken(user4, LARGE_STAKE);
        dealToken(user5, LARGE_STAKE);
        dealToken(user6, LARGE_STAKE);
        dealToken(user7, LARGE_STAKE);
        dealToken(user8, LARGE_STAKE);
        dealToken(user9, LARGE_STAKE);
        dealToken(user10, LARGE_STAKE);
        dealToken(user11, LARGE_STAKE);
        dealToken(user12, LARGE_STAKE);
        dealToken(user13, LARGE_STAKE);
        dealToken(user14, LARGE_STAKE);
        dealToken(user15, LARGE_STAKE);
        dealToken(user16, LARGE_STAKE);
    }

    function dealToken(address to, uint256 amount) internal {
        vm.prank(admin);
        token.transfer(to, amount);
        vm.prank(to);
        token.approve(address(staking), amount);
    }

    function test_wrongBoundariesRecordedWhenStaking() public {
        vm.prank(user1);
        staking.stake(MIN_STAKE);

        vm.prank(user2);
        staking.stake(MIN_STAKE);

        vm.prank(user3);
        staking.stake(MIN_STAKE);

        vm.prank(user4);
        staking.stake(MIN_STAKE);

        vm.prank(user5);
        staking.stake(MIN_STAKE);

        vm.prank(user6);
        staking.stake(MIN_STAKE);

        vm.prank(user7);
        staking.stake(MIN_STAKE);

        vm.prank(user8);
        staking.stake(MIN_STAKE);

        vm.prank(user9);
        staking.stake(MIN_STAKE);

        vm.prank(user10);
        staking.stake(MIN_STAKE);

        vm.prank(user11);
        staking.stake(MIN_STAKE);

        vm.prank(user12);
        staking.stake(MIN_STAKE);

        vm.prank(user13);
        staking.stake(MIN_STAKE);

        vm.prank(user14);
        staking.stake(MIN_STAKE);

        vm.prank(user15);
        staking.stake(MIN_STAKE);

        LayerEdgeStaking.TierEvent[] memory user7History = getTierHistory(user7);

        LayerEdgeStaking.Tier user7TierInHistory;

        for (uint8 i = 0; i < user7History.length; i++) {
            user7TierInHistory = user7History[i].to;
        }

        bool isUser7Tier3InHistory = user7TierInHistory == LayerEdgeStaking.Tier.Tier3;

        LayerEdgeStaking.Tier user7Tier = staking.getCurrentTier(user7);

        bool isUser7Tier2 = user7Tier == LayerEdgeStaking.Tier.Tier2;

        assertEq(isUser7Tier3InHistory, true);
        assertEq(isUser7Tier2, true);
    }

    function getTierHistory(address user) internal view returns (LayerEdgeStaking.TierEvent[] memory) {
        uint256 length = staking.stakerTierHistoryLength(user);

        // Then build the array
        LayerEdgeStaking.TierEvent[] memory history = new LayerEdgeStaking.TierEvent[](length);
        for (uint256 i = 0; i < length; i++) {
            (LayerEdgeStaking.Tier from, LayerEdgeStaking.Tier to, uint256 timestamp) =
                staking.stakerTierHistory(user, i);
            history[i] = LayerEdgeStaking.TierEvent(from, to, timestamp);
        }
        return history;
    }

}
```

### Mitigation

Replace line 897 with line 895 in LayerEdgeStaking.sol