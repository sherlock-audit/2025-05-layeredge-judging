Sneaky Steel Poodle

Medium

# Users with large tier history wont be able to interact with the protocol.

### Summary

The `LayerEdgeStaking.sol`, tracks the tier changes of users for accurate interest calculations. This history of tier changes (`stakerTierHistory`) is used in `calculateUnclaimedInterest()`, which is called by `_updateInterest()`, which is called everytime a user interact with the protocol. If the user has a large `stakerTierHistory`, the functions will run OOG due to hitting the block gas limit. 

In [calculateUnclaimedInterest()](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L397), `stakerTierHistory` looped though multiple times. This will use up enough gas to cause an Out-Of-Gas error if the user has a large enough history.

```solidity
    function calculateUnclaimedInterest(address userAddr) public view returns (uint256 totalInterest) {
        UserInfo memory user = users[userAddr];
        if (user.balance == 0) return user.interestEarned;
        totalInterest = user.interestEarned;
@>       TierEvent[] storage userTierHistory = stakerTierHistory[userAddr];
        if (userTierHistory.length == 0) return totalInterest;
        uint256 fromTime = user.lastClaimTime;
        uint256 toTime = block.timestamp;
        Tier currentTier = Tier.Tier3; 
        uint256 relevantStartIndex = 0;
@>      for (uint256 i = 0; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp <= fromTime) {
                currentTier = userTierHistory[i].to;
                relevantStartIndex = i;
            } else {
                break;
            }
        }
        uint256 periodStart = fromTime;
        uint256 periodEnd;
        if (
            relevantStartIndex + 1 < userTierHistory.length
                && userTierHistory[relevantStartIndex + 1].timestamp < toTime
        ) {
            periodEnd = userTierHistory[relevantStartIndex + 1].timestamp;
        } else {
            periodEnd = toTime;
        }
        if (periodEnd > periodStart) {
            uint256 apy = getTierAPYForPeriod(currentTier, periodStart, periodEnd);
            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
@>      for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp >= toTime) break;
            periodStart = userTierHistory[i].timestamp;
            periodEnd = (i == userTierHistory.length - 1) ? toTime : userTierHistory[i + 1].timestamp;
            if (periodEnd > toTime) periodEnd = toTime;
            if (periodEnd <= periodStart) continue;
            Tier periodTier = userTierHistory[i].to;
            uint256 apy = getTierAPYForPeriod(periodTier, periodStart, periodEnd);
            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
        return totalInterest;
    }
```

### Root Cause

The `stakerTierHistory` is unbounded and can grow big enough to cause OOG error. 

### Internal Pre-conditions

none.

### External Pre-conditions

Enough users stake and unstake for a particular user to reach a large number of history length.

### Attack Path

none.

### Impact

Users with large history wont be able to interact with the protocol, unable to claim interest nor unstake, leaving their funds stuck in the contract.

### PoC

Paste this test in src/test/stake/TierBoundryAndInterestTest.t.sol.

Prerequisites: 
1. import OZ's [String.sol](https://github.com/OpenZeppelin/openzeppelin-contracts/blob/441b1c1c5b909498ca6206a84445ee516067e7fc/contracts/utils/Strings.sol#L13), this is used by the helper function to create new users and stake then unstake to increase charlie's history.
2. Add this helper function, addRemoveUsers(), which creates new users and stake the `MIN_STAKE` to increase charlie's history.

```solidity
import {Strings} from "@openzeppelin/contracts/utils/Strings.sol";

contract TierBoundaryAndInterestTest is Test {
    
    function addRemoveUsers(uint256 num) public {     
        for (uint256 i; i < num; i++) {
            address[] memory users = new address[](num);
            users[i] = makeAddr(Strings.toString(i));
            dealToken(users[i], MIN_STAKE);
            vm.startPrank(users[i]);
            staking.stake(MIN_STAKE); //stake 
            skip(1); //wait 1 block
            staking.unstake(MIN_STAKE); //unstake
            vm.stopPrank();
        }}
```

This is a test showing how much gas it causes for a user(charlie) with 4001 history. 

```solidity
    function test_TierChangeOOG() public {
        vm.pauseGasMetering();
        address[10] memory users = [alice, bob, charlie, dave, eve, frank, grace, heidi, ivan, judy];
        for (uint256 i = 0; i < 6; i++) {
            dealToken(users[i], MIN_STAKE);
            vm.prank(users[i]);
            staking.stake(MIN_STAKE);
        }
        //hisory length
        LayerEdgeStaking.TierEvent[] memory charlieHistory = getTierHistory(charlie);
        console2.log("charlie", charlieHistory.length);        
        //after increasing hisory length
        addRemoveUsers(2000);
        charlieHistory = getTierHistory(charlie);
        console2.log("charlie", charlieHistory.length);
        vm.resumeGasMetering();
        //charlie unstaking gas cost
        vm.prank(charlie);
        uint256 gasBefore = gasleft();
        staking.unstake(MIN_STAKE); 
        uint256 gasAfter = gasleft();
        console2.log("Gas Used", gasBefore - gasAfter);
    }
```
As you can see in the test, it cost 17,188,748 gas, while the current Ethereum gas limit is 35.99M.
When Charlie's history length is ~8380, he wont be able to interact with the protocol.



### Mitigation

_No response_