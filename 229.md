Electric Coal Hornet

High

# `_checkBoundariesAndRecord()` updates the boundaries incorrectly in some case

### Summary
`_checkBoundariesAndRecord()` updates the tier of boundary when `stakerCountInTree` is increased or decreased. However, this function doesn't update the tier of boundary in certain case.


### Root Cause
[`_checkBoundariesAndRecord()`](https://github.com/sherlock-audit/2025-05-layeredge/blob/708c5b5345ed50cd0bde7b0a8a548c8936639683/edgen-staking/src/stake/LayerEdgeStaking.sol#L895) doesn't updates the tier of correct boundry when `new_t2 == old_t2`. When `stakerCountInTree` is increased, it invokes `_findAndRecordTierChange(old_t1 + old_t2, n)` which updates the incorrect rank of user.

Let's check following case:

- current `stakerCountInTree` is 14 and the count of tier1 is 2, tier2 is 4.
- `stakeCountInTree` is increased by 1 and the count of tier1 is 3, tier2 is 4.
    - 1, 1, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 3, 3 : before
    - 1, 1, 1, 2, 2, 2, 2, 3, 3, 3, 3, 3, 3, 3, 3 : after
    
From above array, we can know that rank of 3 and 7 should be updated. Let's consider workflow of the function.

Since `new_t2 = old_t2 = 4` and new staker was added, `_findAndRecordTierChange(old_t1 + old_t2, n)` is invoked, which updates the rank of `old_t1 + old_t2 = 2 + 4 = 6` user. Therefore, the rank of 7 user's tier still remains in tier3 and doesn't update to tier2.

```solidity
    function _checkBoundariesAndRecord(bool isRemoval) internal {
        // recompute thresholds
        uint256 n = stakerCountInTree;
        uint256 oldN = isRemoval ? n + 1 : n - 1;

        // old and new thresholds
        (uint256 old_t1, uint256 old_t2,) = getTierCountForStakerCount(oldN);
        (uint256 new_t1, uint256 new_t2,) = getTierCountForStakerCount(n);

        ...

        // Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
            if (new_t2 != old_t2) {
                // Need to update all users between the old and new tier 2 boundaries
                uint256 old_boundary = old_t1 + old_t2;
                uint256 new_boundary = new_t1 + new_t2;

                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 2 count stays the same
            else if (isRemoval) {
                _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
>>              _findAndRecordTierChange(old_t1 + old_t2, n);
            }
        }
    }
```

### Internal pre-conditions

When new staker is added, in case of current `stakerCountInTree = 14 + 10k` (k = 0, 1, 2, ...).

### External pre-conditions

None

### Attack Path


### Impact

Staker at the boundary doesn't update to upper tier and as result, staker receives low rewards.

### PoC

None

### Mitigation

```diff
    function _checkBoundariesAndRecord(bool isRemoval) internal {
        // recompute thresholds
        uint256 n = stakerCountInTree;
        uint256 oldN = isRemoval ? n + 1 : n - 1;

        // old and new thresholds
        (uint256 old_t1, uint256 old_t2,) = getTierCountForStakerCount(oldN);
        (uint256 new_t1, uint256 new_t2,) = getTierCountForStakerCount(n);

        ...

        // Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
-           if (new_t2 != old_t2) {
+               uint256 old_boundary = old_t1 + old_t2;
+               uint256 new_boundary = new_t1 + new_t2;
+           if (old_boundary != new_boundary) {
-                // Need to update all users between the old and new tier 2 boundaries
-                uint256 old_boundary = old_t1 + old_t2;
-                uint256 new_boundary = new_t1 + new_t2;

                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 2 count stays the same
            else if (isRemoval) {
                _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1 + old_t2, n);
            }
        }
    }
```