Gorgeous Clay Crow

Medium

# Incorrect Tier Assignments Due to Unbounded Staker Counting in the Layeredgestaking contract

### Summary

The absence of a validation check for `nextJoinId` against `MAX_USERS` in the `_stake` function, combined with the Fenwick Tree’s `update` function silently skipping updates for invalid indices, will cause incorrect tier assignments for stakers as the contract counts invalid users in `stakerCountInTree`, inflating tier boundaries and misassigning tiers based on mismatched ranks.

### Root Cause

The `LayerEdgeStaking` contract sets `MAX_USERS = 100,000,000` as the maximum number of stakers the Fenwick Tree can track or the protocol can accept
```solidity
uint256 public constant MAX_USERS = 100_000_000;
```
This limit is enforced in the Fenwick Tree’s initialization in LayerEdgeStaking contract during the initialize function, setting `stakerTree.size = MAX_USERS`
```solidity
function initialize(address _stakingToken, address _admin) public initializer {
    ...
    stakerTree.size = MAX_USERS; // 100,000,000
    nextJoinId = 1;
    ...
}
```
However, a flaw exists in the `_stake function`, where the contract assigns `user.joinId = nextJoinId++` and increments `stakerCountInTree` without checking if `nextJoinId <= MAX_USERS:`
```solidity
 function _stake(uint256 amount, address userAddr, bool isNative) internal {
        require(amount > 0, "Cannot stake zero amount");

        UserInfo storage user = users[userAddr];

        // Update interest before changing balance
        _updateInterest(userAddr);

        // Transfer tokens from user to contract
        if (!isNative) {
            require(stakingToken.transferFrom(userAddr, address(this), amount), "Token transfer failed");
        } else {
            IWETH(address(stakingToken)).deposit{value: amount}();
        }

        Tier tier = Tier.Tier3;

        //Staking for first time and amount is less than minStakeAmount, user will be in tier 3 permanently and out of the tree
        if (!user.isActive && amount < minStakeAmount) {
            stakerCountOutOfTree++;
            user.isActive = true;
            user.outOfTree = true;
            _recordTierChange(userAddr, tier);
        }

        // If first time staking, register staker position whose stake is more than minStakeAmount
        if (!user.isActive && amount >= minStakeAmount) {
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);
            _checkBoundariesAndRecord(false);
        }

        // Update user balances
        user.balance += amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked += amount;

        emit Staked(userAddr, amount, tier);
    }
```

This allows `nextJoinId` to exceed 100,000,000, generating invalid `joinIds` for users beyond the `MAX_USERS` limit. When `joinId > MAX_USERS`, the Fenwick Tree’s update function fails to add these users to the tree due to its loop condition `while (index <= self.size):`

If `index (i.e., joinId)` exceeds `self.size (100,000,000)`, the loop does not execute, resulting in a silent no-op without updating the tree’s sums or reverting. Despite this failure, `_stake` proceeds to increment `stakerCountInTree`, creating a state mismatch where `stakerCountInTree` = `N + M` (`N valid users with joinId ≤ MAX_USERS, M invalid users with joinId > MAX_USERS`), while the Fenwick Tree only tracks N users. This mismatch propagates to `_checkBoundariesAndRecord`, which uses `stakerCountInTree` to compute tier boundaries. `_computeTierByRank` also miscalculates tiers since it uses `stakerCountInTree`

The getTierCountForStakerCount function calculates boundaries using stakerCount
https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L480C4-L509C6

```solidity
 function getTierCountForStakerCount(uint256 stakerCount)
        public
        pure
        returns (uint256 tier1Count, uint256 tier2Count, uint256 tier3Count)
    {
        // Calculate tier 1 count (20% of active stakers)
        tier1Count = (stakerCount * TIER1_PERCENTAGE) / 100;

        // Ensure at least 1 staker in tier 1 if there are any active stakers
        if (tier1Count == 0 && stakerCount > 0) {
            tier1Count = 1;
        }

        // Calculate remaining stakers after tier 1
        uint256 remainingAfterTier1 = stakerCount > tier1Count ? stakerCount - tier1Count : 0;

        // Calculate tier 2 count (30% of total active stakers, but don't exceed remaining)
        uint256 calculatedTier2Count = (stakerCount * TIER2_PERCENTAGE) / 100;

        if (calculatedTier2Count == 0 && remainingAfterTier1 > 0) {
            tier2Count = 1;
        } else {
            tier2Count = calculatedTier2Count > remainingAfterTier1 ? remainingAfterTier1 : calculatedTier2Count;
        }

        // Tier 3 is everyone else
        tier3Count = stakerCount > (tier1Count + tier2Count) ? stakerCount - tier1Count - tier2Count : 0;

        return (tier1Count, tier2Count, tier3Count);
    }
```

Since stakerCount = N + M, boundaries are inflated, but ranks from are capped at N, causing misaligned tier assignments. Invalid users get rank = 0 (Tier 3), while existing users face wrong promotions or demotions. 


### Internal Pre-conditions

The contract is initialized with `MAX_USERS = 100,000,000` and `stakerTree.size = MAX_USERS.`

Users stake `amounts ≥ minStakeAmount (3000 * 10^18 tokens)`, triggering `_stake` to assign new `joinIds`

`nextJoinId` exceeds `MAX_USERS` due to repeated staking without a cap check, generating invalid `joinIds`.

`stakerCountInTree` increments for each `staker`, including those with invalid `joinIds`.



### External Pre-conditions

N/A

### Attack Path

N/A

### Impact

Inflated `stakerCountInTree` increases tier boundaries in `getTierCountForStakerCount`

The FCFS tier system fails, as existing users face unpredictable promotions or demotions 

### PoC

_No response_

### Mitigation

Validate `nextJoinId` in `_stake` and ensure `joinId ≤ MAX_USERS`, preventing invalid users 