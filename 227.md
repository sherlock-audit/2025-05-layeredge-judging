Itchy Red Rabbit

High

# Wrong logic implement in `_checkBoundariesAndRecord`, the function updates wrong rank for Tier 2 user

### Summary

Wrong logic implement in `_checkBoundariesAndRecord`, the function updates wrong rank for Tier 2 user

### Root Cause

At the end of the function `_checkBoundariesAndRecord`, when it comes to updating for users in Tier 2. This function call the `_findAndRecordTierChange` function to update, but the rank of user is wrong.

Code: [LayerEdgeStaking.sol#L894-L898](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L894-L898)

```solidity
            else if (isRemoval) {
                _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1 + old_t2, n);
            }
```

With the condition `isRemoval` is `true`, then `new_t1 + new_t2 <= old_t1 + old_t2`, leading to the rank that needs to be updated is `old_t1 + old_t2` because this rank is moved from Tier2 to Tier3. But the code updates rank `new_t1 + new_t2`.

Similarly, if `isRemoval` is `false`, then `new_t1 + new_t2 >= old_t1 + old_t2`, the rank that needs to be updated is `new_t1 + new_t2`. But the code updates rank `old_t1 + old_t2`.

### Internal Pre-conditions

-   User stake or unstake.

### External Pre-conditions

N/A

### Attack Path

N/A

### Impact


-   When `_stakerCountInTree` changes in a way that makes `new_t1 > old_t1` and `new_t2 = old_t2`, or `new_t1 < old_t1` and `new_t2 = old_t2`. The function will update the wrong Tier2.

For example in the table below:

-   When `_stakerCountInTree` increases from 14 to 15, the user at rank 7 must be updated to Tier2. But according to the code, this user will still stay in Tier3.
-   And vice versa, when `_stakerCountInTree` decreases from 15 to 14. user at rank 7 has to move to Tier3. But the code still keeps Tier2 for this user.

| n   | Tier1 | Tier2 |
| --- | ----- | ----- |
| 14  | 2     | 4     |
| 15  | 3     | 4     |
| ... |       |       |
| 24  | 4     | 7     |
| 25  | 5     | 7     |
| ... |       |       |
| 34  | 6     | 10    |
| 35  | 7     | 10    |

-   More generally, when `_stakerCountInTree` = 10*k + 4 (k > 0), increases to 10*k+5, or decreases from 10*k+5 to 10*k+4. `_checkBoundariesAndRecord` will update Tier2 incorrectly.

| n           | Tier1      | Tier2    |
| ----------- | ---------- | -------- |
| 10 \* k + 4 | 2 \* k + 0 | 3\*k + 1 |
| 10 \* k + 5 | 2 \* k + 1 | 3\*k + 1 |

### PoC


-   Place this code into `LayerEdgeStakingTest.t.sol` and run `forge test --mt test_checkBoundariesAndRecord_wrongTier`. Test passes.
-   The 7th staker is `grace`. Still in Tier3 at the end.

```solidity
    function test_checkBoundariesAndRecord_wrongTier() public {
        // Set up 10 stakers
        setupMultipleStakers(10);

        // Setup 5 user more
        address user_11 = makeAddr("user_11");
        address user_12 = makeAddr("user_12");
        address user_13 = makeAddr("user_13");
        address user_14 = makeAddr("user_14");
        address user_15 = makeAddr("user_15");

        vm.startPrank(alice);
        token.transfer(user_11, MIN_STAKE);
        token.transfer(user_12, MIN_STAKE);
        token.transfer(user_13, MIN_STAKE);
        token.transfer(user_14, MIN_STAKE);
        token.transfer(user_15, MIN_STAKE);
        vm.stopPrank();

        address[5] memory stakers_2 = [
            user_11,
            user_12,
            user_13,
            user_14,
            user_15
        ];

        for (uint256 i = 0; i < 5; i++) {
            vm.startPrank(stakers_2[i]);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
            vm.stopPrank();
        }

        // Check tier counts
        (uint256 tier1Count, uint256 tier2Count, uint256 tier3Count) = staking
            .getTierCounts();
        assertEq(tier1Count, 3); // 20% of 15 = 3
        assertEq(tier2Count, 4); // 30% of 15 = 4 => the 7th staker must be Tier2
        assertEq(tier3Count, 8); // Remaining 8

        // grace is the 7th staker, should be in Tier2 follow calculation above. But she's in Tier3 now.
        assertEq(
            uint256(staking.getCurrentTier(grace)),
            uint256(LayerEdgeStaking.Tier.Tier3)
        );
    }
```

### Mitigation

-   Change this code:

```diff
            else if (isRemoval) {
-               _findAndRecordTierChange(new_t1 + new_t2, n);
+               _findAndRecordTierChange(old_t1 + old_t2, n);
            } else if (!isRemoval) {
-               _findAndRecordTierChange(old_t1 + old_t2, n);
+               _findAndRecordTierChange(new_t1 + new_t2, n);
            }
```
