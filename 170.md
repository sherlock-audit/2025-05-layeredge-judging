Proper Citron Dachshund

High

# `LayerEdgeStaking::_checkBoundariesAndRecord` breaks Tier distribution correctness

### Summary

Incorrect handling of cases where Tier 2 count stays the same but one user leaves Tier 2 and one user moves to Tier 2 will cause a 15% APY loss for the user or protocol.

### Root Cause

`LayerEdgeStaking::_checkBoundariesAndRecord` function does not correctly handle cases where Tier 2 count stays the same but one user leaves Tier 2 and one user moves to Tier 2.

The condition in [`LayerEdgeStaking:876`](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L876)  only checks that the new and old thresholds for Tier 2 are not equal, and this results in one user not being promoted from Tier 3 to Tier 2 (in case `stakerCountInTree` increases) or one user not being demoted from Tier 2 to Tier 3 (in case `stakerCountInTree` decreases).

```solidity
// Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
@>          if (new_t2 != old_t2) {
                // Need to update all users between the old and new tier 2 boundaries
                uint256 old_boundary = old_t1 + old_t2;
                uint256 new_boundary = new_t1 + new_t2;

                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 2 count stays the same
            else if (isRemoval) {
                _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1 + old_t2, n);
            }
        }
    }
```

Note: getter functions (`getCurrentTier`, `getUserAPY`, `getTierCounts`, `getTierCountForStakerCount`, `getUserInfo`, `getAllInfoOfUser`, `getAllStakingInfo`) may return incorrect (more precisely, not corresponding to the state) values ​​of tier counts and APY rates. For example, the function `getTierCountForStakerCount` always shows the "correct" and not the actual counts of users in Tiers. And the function `getUserAPY` shows the APY value that the user should have. For example, if a user mistakenly stayed in Tier 3 and was not moved to Tier 2, then `getUserAPY` will return a value of 35%, not 20%, but the 20% value will be used when calculating the reward. But the root cause is in `_checkBoundariesAndRecord`, not in getter functions.

### Internal Pre-conditions

1. The vulnerability path happens always when `stakerCountInTree` becomes equal to {15, 25, 35, 45, 55, ... }. This happens both when decreasing and increasing `stakerCountInTree` — for example (14 -> 15) and (15 -> 14).

### External Pre-conditions

None

### Attack Path

None

### Impact

With default APY values ​​of 35% and 20% for Tier 2 and Tier 3, respectively:
- Users who are not promoted from Tier 3 to Tier 2 suffer a loss of 15% APY.
- Users who are not demoted from Tier 2 to Tier 3 gains 15% more APY (meaning the protocol pays more and therefore suffers a loss).

### PoC
Paste in `LayerEdgeStakingTest.t.sol`:
```solidity
function test_breakTiersDistribution() public {
        uint256 n = 15; //amount of stakers
        uint256 amount = staking.minStakeAmount();
        address staker;
        address[] memory stakers = new address[](n);
        // n users staked minStakeAmount
        for (uint256 i; i < n; i++) {
            staker = makeAddr(string(abi.encodePacked(i + 1)));
            stakers[i] = staker;
            // Distribute tokens to user
            vm.startPrank(admin);
            token.transfer(staker, amount);
            vm.stopPrank();
            //stake
            vm.startPrank(staker);
            token.approve(address(staking), amount);
            staking.stake(amount);
            vm.stopPrank();
        }
        // Advance time past unstaking window
        vm.warp(block.timestamp + 7 days + 1);

        for (uint256 i; i < stakers.length; i++) {
            (
                LayerEdgeStaking.UserInfo memory user,
                LayerEdgeStaking.Tier tier,
                uint256 apy,
                uint256 pendingRewards,
                LayerEdgeStaking.TierEvent[] memory tierHistory
            ) = staking.getAllInfoOfUser(stakers[i]);

            console2.log("Staker #", i+1, stakers[i]);
            // NOTE: read tierHistory because it contains true tier info
            // true Tiers
            if (tierHistory[tierHistory.length - 1].to == LayerEdgeStaking.Tier.Tier1) {
                console2.log("tierHistory: staker is in Tier 1");
            } else if (tierHistory[tierHistory.length - 1].to == LayerEdgeStaking.Tier.Tier2) {
                console2.log("tierHistory: staker is in Tier 2");
            } else {
                 console2.log("tierHistory: staker is in Tier 3");
            }
            // false Tiers
            if (tier == LayerEdgeStaking.Tier.Tier1) {
                console2.log("getAllInfoOfUser: staker is in Tier 1");
            } else if (tier == LayerEdgeStaking.Tier.Tier2) {
                console2.log("getAllInfoOfUser: staker is in Tier 2");
            } else {
                console2.log("getAllInfoOfUser: staker is in Tier 3");
            }
            console2.log("getAllInfoOfUser: user's APY = ", apy);
            console2.log("getAllInfoOfUser: user's pendingRewards = ", pendingRewards);
            console2.log("-------------------------------");
        }
    }
```

Pay attention to `Staker # 7` tier and rewards: 
```solidity
 Staker # 6 0x1cF85De2e11B6574584489a48D2A4020Eef60E67
  tierHistory: staker is in Tier 2
  getAllInfoOfUser: staker is in Tier 2
  getAllInfoOfUser: user's APY =  35000000000000000000
  getAllInfoOfUser: user's pendingRewards =  20137019596651445966
  -------------------------------
  Staker # 7 0x58a772A130b03f3Cc065165F936D14E655a07bFd
  tierHistory: staker is in Tier 3
  getAllInfoOfUser: staker is in Tier 2
  getAllInfoOfUser: user's APY =  35000000000000000000
  getAllInfoOfUser: user's pendingRewards =  11506868340943683409
  -------------------------------
  Staker # 8 0x12fec127100A46e81BD09C1b07cF3D0F0C0de070
  tierHistory: staker is in Tier 3
  getAllInfoOfUser: staker is in Tier 3
  getAllInfoOfUser: user's APY =  20000000000000000000
  getAllInfoOfUser: user's pendingRewards =  11506868340943683409
```

### Mitigation

Change the condition in [`LayerEdgeStaking:876`](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L876):

```diff
 // Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
-           if (new_t2 != old_t2) {
+           if (new_t1 > old_t1 || new_t1 < old_t1 || new_t2 != old_t2  ) { 
                // Need to update all users between the old and new tier 2 boundaries
                uint256 old_boundary = old_t1 + old_t2;
                uint256 new_boundary = new_t1 + new_t2;

                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
```