Shaggy Mandarin Chicken

Invalid

# Permanent Exclusion from Tier System After Balance Reduction.

### Summary

The LayerEdgeStaking.sol:674-723 contract contains a critical logic flaw that permanently excludes users from the tier ranking system if their balance ever drops below minStakeAmount. Once excluded, users can NEVER re-enter the tier tree, even if they stake large amounts afterward. This results in permanent assignment to Tier 3.

### Root Cause

The isActive flag is set to true when user call the stake function for the first time ,but isActive flag is NEVER reset to false. The contract only allows users to enter the tier tree when (!user.isActive && amount >= minStakeAmount), creating an impossible condition for previously active users.

```solidity
function _stake(uint256 amount, address userAddr, bool isNative) internal {
        require(amount > 0, "Cannot stake zero amount");

        UserInfo storage user = users[userAddr];

        // Update interest before changing balance
        _updateInterest(userAddr);

        // Transfer tokens from user to contract
        if (!isNative) {
            require(stakingToken.transferFrom(userAddr, address(this), amount), "Token transfer failed");
        } else {
            IWETH(address(stakingToken)).deposit{value: amount}();
        }

        Tier tier = Tier.Tier3;

        //Staking for first time and amount is less than minStakeAmount, user will be in tier 3 permanently and out of the tree
        if (!user.isActive && amount < minStakeAmount) {
            stakerCountOutOfTree++;
            user.isActive = true; // @note this variable set only hear 
            user.outOfTree = true;
            _recordTierChange(userAddr, tier);
        }

        // If first time staking, register staker position whose stake is more than minStakeAmount
        if (!user.isActive && amount >= minStakeAmount) {
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);
            _checkBoundariesAndRecord(false);
        }

        // Update user balances
        user.balance += amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked += amount;

        emit Staked(userAddr, amount, tier);
    }
```
https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L674C5-L723C6

### Internal Pre-conditions

1.User should call the stake function with large amount to achive Tier 1 or Tier2 .
2.After User should call unstake function with amount which is equal to his all balance or some amount after that the remaining balance is less then the minStakeAmount.
3.At the end User just try to call the stake function again with larger amout but he still stay on a Tier3.

### External Pre-conditions

There are no External Pre-conditions.

### Attack Path

1.Victim stakes 5,000 tokens and achieves Tier 1 status (50% APY)
2.Victim needs liquidity and unstakes 2,500 tokens, leaving 2,500 (below 3,000 minimum)
3.Victim is moved to outOfTree = true but isActive remains true
4.Victim later stakes 9,000 tokens expecting to return to Tier 1
5.Victim is permanently stuck in Tier 3 (20% APY) despite having 11,000 tokens stake

### Impact

The affected stakers cannot re-enter the tier system regardless of stake size, resulting in permanent financial disadvantage.Competing stakers gain an unfair advantage through reduced competition for Tier 1/2 positions, potentially earning 15-30% higher APY.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import "forge-std/Test.sol";
import "../../src/stake/LayerEdgeStaking.sol";
import "../mock/LayerEdgeToken.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";


contract LayerEdgeStakingPrecisionTest is Test {
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;
    
    address public admin =  makeAddr("admin");
    address public alice =  makeAddr("alice");
    address public bob = makeAddr("bob");
    address public charlie = makeAddr("charlie");
    address public whale = makeAddr("whale");
    
    uint256 constant PRECISION = 1e18;
    uint256 constant SECONDS_IN_YEAR = 365 days;
    uint256 constant MIN_STAKE = 3000 * 1e18;
    
    function setUp() public {
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();

        token = LayerEdgeToken(config.stakingToken);
        admin = config.owner;
        
        vm.startPrank(admin);
        
        // Fund test accounts
        uint256 userAmount = 100_000 * 1e18;
        token.transfer(alice,userAmount );
        // token.transfer(bob, userAmount);
        // token.transfer(charlie, userAmount);
        // token.transfer(whale, userAmount);

        uint256 rewardAmount = 100_000 * 1e18; 
        token.approve(address(staking), rewardAmount);
        staking.depositRewards(rewardAmount);
        staking.setCompoundingStatus(true);
        staking.setMinStakeAmount(MIN_STAKE);
        vm.stopPrank();
        
        // Approve staking contract
        vm.prank(alice);
        token.approve(address(staking), (type(uint256).max - 1));
        // vm.prank(bob);
        // token.approve(address(staking), (type(uint256).max - 1));
        // vm.prank(charlie);
        // token.approve(address(staking), (type(uint256).max -1));
        // vm.prank(whale);
        // token.approve(address(staking), (type(uint256).max - 1));
    }

    function testPermanentExclusionBug() public {
        console.log("\n=== CRITICAL BUG: Permanent Tree Exclusion ===");
        
        uint256 initialStake = 5000 * 1e18;
        uint256 unstakeAmount = 2500 * 1e18;
        uint256 restakeAmount = 9000 * 1e18;
        
        // Step 1: Alice stakes above minimum
        vm.prank(alice);
        staking.stake(initialStake);
        
        // Verify Alice is in tree
        LayerEdgeStaking.Tier aliceTier = staking.getCurrentTier(alice);
        console.log("After initial stake - Tier: %s", uint(aliceTier));
        require(aliceTier != LayerEdgeStaking.Tier.Tier3, "Should not be Tier 3");
        
        // Check state
        (uint256 balance1,,,,,, bool outOfTree1, bool isActive1,) = staking.users(alice);
        console.log("Balance: %s, OutOfTree: %s, IsActive: %s", balance1, outOfTree1, isActive1);
        
        // Step 2: Alice unstakes, bringing balance below minimum
        vm.warp(block.timestamp + 7 days);
        vm.prank(alice);
        staking.unstake(unstakeAmount);
        
        // Complete unstake
        vm.warp(block.timestamp + 7 days);
        vm.prank(alice);
        staking.completeUnstake(0);
        
        // Check state after unstake
        (uint256 balance2,,,,,, bool outOfTree2, bool isActive2,) = staking.users(alice);
        console.log("\nAfter unstake - Balance: %s, OutOfTree: %s, IsActive: %s", 
            balance2, outOfTree2, isActive2);
        
        // BUG: isActive is still true!
        require(isActive2 == true, "isActive should still be true");
        require(outOfTree2 == true, "Should be out of tree");
        
        // Step 3: Alice tries to stake again above minimum
        vm.prank(alice);
        staking.stake(restakeAmount);
        
        // Check final state
        (uint256 balance3,,,,,, bool outOfTree3, bool isActive3,) = staking.users(alice);
        LayerEdgeStaking.Tier finalTier = staking.getCurrentTier(alice);
        
        
        // BUG CONFIRMED: User is permanently stuck in Tier 3!
        require(outOfTree3 == true, "Still out of tree!");
        require(finalTier == LayerEdgeStaking.Tier.Tier3, "Permanently in Tier 3!");
        
        console.log("BUG CONFIRMED: User can NEVER return to tree!");
    }      
}    
```

### Mitigation

Solution 1: Reset isActive on Full Unstake

```solidity
function _unstake(uint256 amount, address userAddr) internal {
    UserInfo storage user = users[userAddr];
    require(user.balance >= amount, "Insufficient balance");
    
    _updateInterest(userAddr);
    
    user.balance -= amount;
    user.lastClaimTime = block.timestamp;
    totalStaked -= amount;

    if (!user.outOfTree && user.balance < minStakeAmount) {
        _recordTierChange(userAddr, Tier.Tier3);
        stakerTree.update(user.joinId, -1);
        stakerCountInTree--;
        user.outOfTree = true;
        stakerCountOutOfTree++;
        _checkBoundariesAndRecord(true);
    }
    
    // FIX: Reset isActive when balance is 0
    if (user.balance == 0) {
        user.isActive = false;
        user.isFirstDepositMoreThanMinStake = false;
    }

    unstakeRequests[userAddr].push(UnstakeRequest({
        amount: amount,
        timestamp: block.timestamp,
        completed: false
    }));

    emit UnstakedQueued(userAddr, unstakeRequests[userAddr].length - 1, amount);
}
```
Solution 2: Allow Re-entry to Tree 

```solidity
function _stake(uint256 amount, address userAddr, bool isNative) internal {
    require(amount > 0, "Cannot stake zero amount");
    
    UserInfo storage user = users[userAddr];
    _updateInterest(userAddr);
    
    if (!isNative) {
        require(stakingToken.transferFrom(userAddr, address(this), amount), "Token transfer failed");
    } else {
        IWETH(address(stakingToken)).deposit{value: amount}();
    }
    
    Tier tier = Tier.Tier3;
    
    // Handle users who were removed from tree but want to re-enter
    if (user.isActive && user.outOfTree && user.balance + amount >= minStakeAmount) {
        // Re-enter the tree
        user.joinId = nextJoinId++;
        stakerTree.update(user.joinId, 1);
        stakerAddress[user.joinId] = userAddr;
        user.outOfTree = false;
        stakerCountOutOfTree--;
        stakerCountInTree++;
        
        uint256 rank = stakerTree.query(user.joinId);
        tier = _computeTierByRank(rank, stakerCountInTree);
        
        _recordTierChange(userAddr, tier);
        _checkBoundariesAndRecord(false);
    }
    // Existing logic for new users
    else if (!user.isActive && amount >= minStakeAmount) {
        // ... existing code ...
    }
    
    user.balance += amount;
    user.lastClaimTime = block.timestamp;
    totalStaked += amount;
    
    emit Staked(userAddr, amount, tier);
}
```