Howling Daffodil Bee

High

# User can't claim/compound only a part of its earned interests

### Summary

When calling ``claimInterest()``, ``claimInterestNative()`` or ``compoundInterest()``, which are the only way to use/withdraw a user ``interestEarned``, there is the check : 
```solidity
require(rewardsReserve >= claimable, "Insufficient rewards in contract");
```

This check requires that the user earned interests amount is lower (or equal) than the reward reserve of the contract. So a user can't claim/compound part of its earned interests.

### Root Cause

In ``LayerEdgeStaking.sol`` in the function [_claimInterest()](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L788) and in the function [compoundInterest()](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L222) there is the following check:
```solidity
require(rewardsReserve >= claimable, "Insufficient rewards in contract");
```

There is no way for the user to use (claim/compound) only part of its earned interests.

### Internal Pre-conditions

User must have its variable ``interestEarned`` being greater than the contract's ``rewardsReserve``.

### External Pre-conditions

None.

### Attack Path

None, not an attack.

### Impact

### Basic
User can't claim/compound its earned interests if they are higher than the reward reserve of the contract. 

## Unfair effect

Users with a small amount of earned interests will be more likely to be able to claim/compound their earned interests, meanwhile users with a big amount of earned interests (so generally a big balance and a lot of staking, so more helpful to the protocol) will be more affected.

## Snowball effect

If the user can't claim/compound its earned interests because of this, then he'll earn even more interests and the reward reserve would need to be even higher for him to claim/compound them.

Also, users with smaller earned interests amount will be able to claim/compound theirs in the meantime, which will lower the current ``rewardsReserve`` and require it to be even more re-funded for the users with a lot of earned interests being able to claim/compound theirs. Creating a spiral effect.

### PoC

_No response_

### Mitigation

Allow the user to specify an amount of earned interests he wants to claim/compound. Could be done as such for both functions mentionned :

```solidity
    /**
     * @notice Compound interest by adding it to staked balance
     */
    function compoundInterest(uint256 amount) external whenCompoundingEnabled nonReentrant whenNotPaused {
        _updateInterest(msg.sender);

        UserInfo storage user = users[msg.sender];
        uint256 claimable = user.interestEarned;

        require(claimable > 0, "Nothing to compound");

        require(claimable >= amount, "Not enough interests."); // New line

        // Check if we have enough rewards in the contract
        // require(rewardsReserve >= claimable, "Insufficient rewards in contract"); Old line
        require(rewardsReserve >= amount, "Insufficient rewards in contract"); // new line

        // Update rewards reserve
        rewardsReserve -= amount;

        // Add earned interest to staked balance
        user.balance += amount;
        totalStaked += amount;

        // Update interest tracking
        user.lastClaimTime = block.timestamp;
        user.interestEarned -= amount;
        user.totalClaimed += amount;

        // Determine user's tier for event (might have changed due to increased balance)
        Tier tier = getCurrentTier(msg.sender);

        emit RewardClaimed(msg.sender, amount);
        emit Staked(msg.sender, amount, tier);
    }
```

And for the other function (the ``amount`` parameter has to be added to ``claimInterest()`` and ``claimInterestNative()``):

```solidity
    function _claimInterest(address userAddr, bool isNative, uint256 amount) internal {
        _updateInterest(userAddr);

        UserInfo storage user = users[userAddr];
        uint256 claimable = user.interestEarned;

        require(claimable > 0, "Nothing to claim");

        require(claimable >= amount, "Not enough interests."); // New line

        // Check if we have enough rewards in the contract
        // require(rewardsReserve >= claimable, "Insufficient rewards in contract"); Old line
        require(rewardsReserve >= amount, "Insufficient rewards in contract"); // new line

        user.lastClaimTime = block.timestamp;
        user.interestEarned -= amount;
        user.totalClaimed += amount;

        // Update rewards reserve
        rewardsReserve -= amount;

        // Transfer tokens to user
        if (!isNative) {
            require(stakingToken.transfer(userAddr, amount), "Token transfer failed");
        } else {
            IWETH(address(stakingToken)).withdraw(amount);
            (bool success,) = payable(userAddr).call{value: amount}("");
            require(success, "Claim interest native transfer failed");
        }

        emit RewardClaimed(userAddr, amount);
    }
```

There are other possible mitigations, such as setting the amount claimes/compound to ``rewardsReserve`` value if it exceeds it.