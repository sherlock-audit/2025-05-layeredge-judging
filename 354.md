Joyous Fuzzy Trout

Medium

# Permanent Tier 3 Assignment Vulnerability Due to Initial Low-Stake Lock-in

### Summary

Users who initially stake an amount below the `minStakeAmount` are permanently locked into **Tier 3** and marked as `outOfTree`. This prevents them from being upgraded to higher tiers (Tier 1 or Tier 2), even if their subsequent cumulative stakes exceed the `minStakeAmount`. This disrupts the intended reward distribution mechanism, reducing incentives for additional staking and causing unfair reward allocation.

### Root Cause

The logic in the [`stake`](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L674) function permanently assigns `outOfTree = true` when a user's initial stake is below `minStakeAmount`. This flag prevents the user from being included in the ranking tree (`stakerTree`) in subsequent staking actions. As a result:
- The user is excluded from tier upgrades.
- Their staking rank is never recalculated.

### Internal Pre-conditions

NA

### External Pre-conditions

NA

### Attack Path

1. A user stakes 1000 tokens (below `minStakeAmount`) to intentionally lock themselves into Tier 3.
2. They then stake an additional 10,000 tokens, but remain in Tier 3.
3. This allows the user to avoid higher APY and reduce their rewards, potentially for external tax or regulatory reasons.

### Impact

1. **For Users:**
   - Users unintentionally locked in Tier 3 will earn lower APY rewards, even if their stake qualifies for Tier 1 or Tier 2.
   - Users aiming to maximize rewards may lose trust in the system due to unfair tier assignments.

2. **For the Protocol:**
   - Rewards distribution becomes imbalanced, as high-stake users locked in Tier 3 are excluded from Tier 1 and Tier 2 reward pools.
   - The protocol's incentive mechanism (encouraging higher stakes) is undermined.

### PoC

```solidity
    function test_TierAssignment_LowInitialStake_LockedInTier3() public {
        // Alice stakes minimum amount
        vm.startPrank(alice);

        uint256 stakeAmount = MIN_STAKE / 2;
        token.approve(address(staking), stakeAmount);
        vm.expectEmit(true, true, true, true);
        emit LayerEdgeStaking.Staked(alice, stakeAmount, LayerEdgeStaking.Tier.Tier3); // First staker should be tier 3
        staking.stake(stakeAmount);
        vm.stopPrank();

        // Check tier counts with one staker
        (uint256 tier1Count, uint256 tier2Count, uint256 tier3Count) = staking.getTierCounts();
        assertEq(tier1Count, 0);
        assertEq(tier2Count, 0);
        assertEq(tier3Count, 0);

        // Check Alice's tier and APY
        (, LayerEdgeStaking.Tier tier, uint256 apy,) = staking.getUserInfo(alice);
        assertEq(uint256(tier), uint256(LayerEdgeStaking.Tier.Tier3));
        assertEq(apy, 20 * PRECISION);

        // Check token balances
        assertEq(token.balanceOf(address(staking)), 100_000 * 1e18 + stakeAmount); // Rewards + stake
        assertEq(token.balanceOf(alice), 100_000 * 1e18 - stakeAmount);

        // Stake again
        vm.startPrank(alice);
        stakeAmount = MIN_STAKE * 2;
        token.approve(address(staking), stakeAmount);
        vm.expectEmit(true, true, true, true);
        emit LayerEdgeStaking.Staked(alice, stakeAmount, LayerEdgeStaking.Tier.Tier3);// Second stake is also in Tire3, but should in Tire1
        staking.stake(stakeAmount);
        vm.stopPrank();
    }
```

### Mitigation

1. **Dynamic Reassignment to Ranking Tree:**
   Modify the `stake` function to re-add users to the `stakerTree` if their total balance exceeds `minStakeAmount` after subsequent stakes:
   ```solidity
   if (user.outOfTree && user.balance + amount >= minStakeAmount) {
       user.outOfTree = false;
       user.joinId = nextJoinId++;
       stakerTree.update(user.joinId, 1);
       stakerCountOutOfTree--;
       stakerCountInTree++;
       _recordTierChange(userAddr, _computeTierByRank(user.joinId, stakerCountInTree));
   }
   ```

2. **Remove Permanent Lock-in:**
   Ensure that `outOfTree` is used only temporarily and re-evaluated dynamically based on the user's balance.

3. **Recalculate Tier on Stake:**
   Always validate and adjust the user's tier on subsequent stakes or balance changes.