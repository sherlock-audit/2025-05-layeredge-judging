Gigantic Carrot Cheetah

Medium

# The staker at the boundary remains in tier2 in certain case

### Summary
The function `_checkBoundariesAndRecord()` adjusts the tier of boundary rank user based on `stakerCountInTree` changes. However, in some situations, it fails to correctly update incorrect rank user's tier.

### Root Cause
Let's check following case;

Let's assume `stakerCountInTree` is 15 now. At the moment number of tier1 is 3 and tier2 is 4.

Then staker(whose tier is tier3) in the tree wants to unstake his collateral and `_checkBoundariesAndRecord(true)` is invoked.

```solidity
    function _unstake(uint256 amount, address userAddr) internal {
        ...

        if (!user.outOfTree && user.balance < minStakeAmount) {
            // execute this before removing from tree, this will make sure to calculate interest
            //for amount left after unstake
            _recordTierChange(userAddr, Tier.Tier3);
            stakerTree.update(user.joinId, -1);
            stakerCountInTree--;
            user.outOfTree = true;
            stakerCountOutOfTree++;
@>          _checkBoundariesAndRecord(true);
        }
        ...
    }
```

After a staker unstakes his collateral, `stakerCountInTree` will be 14 and number of tier1 will be 2 and tier2 is 4. In this case, staker whose rank is 3 should be updated to tier2 and staker whose rank is 7 should be updated to tier3.

However, the [`_checkBoundariesAndRecord()`](https://github.com/sherlock-audit/2025-05-layeredge/blob/708c5b5345ed50cd0bde7b0a8a548c8936639683/edgen-staking/src/stake/LayerEdgeStaking.sol#L895) doesn't update the user whose rank is 7 and updates incorrectly rank 6, as you can see in following code.

```solidity
    function _checkBoundariesAndRecord(bool isRemoval) internal {
        // recompute thresholds
        uint256 n = stakerCountInTree;
        uint256 oldN = isRemoval ? n + 1 : n - 1;

        // old and new thresholds
        (uint256 old_t1, uint256 old_t2,) = getTierCountForStakerCount(oldN);
        (uint256 new_t1, uint256 new_t2,) = getTierCountForStakerCount(n);

        // Tier 1 boundary handling
        if (new_t1 != 0) {
            if (new_t1 != old_t1) {
                // Need to update all users between the old and new boundaries
                if (new_t1 > old_t1) {
                    // Promotion case: update all users from old_t1+1 to new_t1
                    for (uint256 rank = old_t1 + 1; rank <= new_t1; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_t1+1 to old_t1
                    for (uint256 rank = new_t1 + 1; rank <= old_t1; rank++) {
                        // this updates the rank of 3
@>                      _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 1 count stays the same
            else if (isRemoval && new_t1 > 0) {
                _findAndRecordTierChange(new_t1, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1, n);
            }
        }

        // Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
            if (new_t2 != old_t2) {
                // Need to update all users between the old and new tier 2 boundaries
                uint256 old_boundary = old_t1 + old_t2;
                uint256 new_boundary = new_t1 + new_t2;

                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 2 count stays the same
            else if (isRemoval) {
                // @audit this updates new_t1 + new_t2 = 2 + 4 = 6 rank of user and staker whose rank of 7 will remain in tier2
@               _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1 + old_t2, n);
            }
        }
    }
```

### Internal pre-conditions
stakerCountInTree = 15, and staker gonna unstake.

### External pre-conditions
None

### Attack Path
None

### Impact
The staker whose tier is not updated to lower tier will receive high APY and the protocol will lose rewards. 

### PoC
None

### Mitigation

```diff
    function _checkBoundariesAndRecord(bool isRemoval) internal {
        // recompute thresholds
        uint256 n = stakerCountInTree;
        uint256 oldN = isRemoval ? n + 1 : n - 1;

        // old and new thresholds
        (uint256 old_t1, uint256 old_t2,) = getTierCountForStakerCount(oldN);
        (uint256 new_t1, uint256 new_t2,) = getTierCountForStakerCount(n);

        // Tier 1 boundary handling
        if (new_t1 != 0) {
            if (new_t1 != old_t1) {
                // Need to update all users between the old and new boundaries
                if (new_t1 > old_t1) {
                    // Promotion case: update all users from old_t1+1 to new_t1
                    for (uint256 rank = old_t1 + 1; rank <= new_t1; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_t1+1 to old_t1
                    for (uint256 rank = new_t1 + 1; rank <= old_t1; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 1 count stays the same
            else if (isRemoval && new_t1 > 0) {
                _findAndRecordTierChange(new_t1, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1, n);
            }
        }

        // Tier 2 boundary handling
        if (new_t1 + new_t2 > 0) {
-           if (new_t2 != old_t2) {
-               // Need to update all users between the old and new tier 2 boundaries
-               uint256 old_boundary = old_t1 + old_t2;
-               uint256 new_boundary = new_t1 + new_t2;

+           uint256 old_boundary = old_t1 + old_t2;
+           uint256 new_boundary = new_t1 + new_t2;

+           if (old_boundary != new_boundary) {
                if (new_boundary > old_boundary) {
                    // Promotion case: update all users from old_boundary+1 to new_boundary
                    for (uint256 rank = old_boundary + 1; rank <= new_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // Demotion case: update all users from new_boundary+1 to old_boundary
                    for (uint256 rank = new_boundary + 1; rank <= old_boundary; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                }
            }
            // Handle case where Tier 2 count stays the same
            else if (isRemoval) {
                _findAndRecordTierChange(new_t1 + new_t2, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1 + old_t2, n);
            }
        }
    }
```