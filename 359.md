Powerful Pecan Parakeet

Medium

# More than the MAX_USERS are allowed to join the staking, this will break the Tier Percentage Invariant.

### Summary

According to the Readme, the `MAX_USERS` is the maximum number of users allowed on the fenwick tree, but this is not accurate, as more than the `MAX_USERS` can technically be on the fenwick tree. When this happens, it breaks the hard-coded Tier percentage of 20%, 30% and 50%, which is a core invariant of the system. While joinId above the `MAX_USERS` are not directly added to the tree, they can take part in staking because they are registered as active stakers and their index is not verified on the `Fenwick:query()` function, so users with joinIds greater than `MAX_USERS`  will take part in the Tier System an break the tier percentage.

### Root Cause

The root cause of this is that the `stakerCountInTree` variable is not compared with the  `MAX_USERS ` value, so when the StakerCountInTree grows above the `MAX_USERS `, it will still be added to the tree, breaking the core system restrictions.

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L700

```solidity
    function _stake(uint256 amount, address userAddr, bool isNative) internal {
        require(amount > 0, "Cannot stake zero amount");

        UserInfo storage user = users[userAddr];

        // Update interest before changing balance
        _updateInterest(userAddr);

        // Transfer tokens from user to contract
        if (!isNative) {
            require(stakingToken.transferFrom(userAddr, address(this), amount), "Token transfer failed");
        } else {
            IWETH(address(stakingToken)).deposit{value: amount}();
        }

        Tier tier = Tier.Tier3;

        //Staking for first time and amount is less than minStakeAmount, user will be in tier 3 permanently and out of the tree
        if (!user.isActive && amount < minStakeAmount) {
            stakerCountOutOfTree++;
            user.isActive = true;
            user.outOfTree = true;
            _recordTierChange(userAddr, tier);
        }

        // If first time staking, register staker position whose stake is more than minStakeAmount
@->        if (!user.isActive && amount >= minStakeAmount) {
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);
            _checkBoundariesAndRecord(false);
        }

        // Update user balances
        user.balance += amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked += amount;

        emit Staked(userAddr, amount, tier);
    }

```

### Internal Pre-conditions

The total active stakers need to go above 100,000,000, which is a very large value.

### External Pre-conditions

None

### Attack Path

Because 100,000,000 is very large, there is no attack path except mass adoption.

### Impact

1. More than the maximum number of users are added to the Tier System, which breaks the design.
2. The Tier Percentage is broken; this is a core Invariant of the system.
3. The FCFS system is broken as subsequent users can rank Higher than previous ones.

### PoC

Because of the gas requirement to run this test and foundry limitations, Change [`MAX_USERS = 100`](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L39). This will make the test possible and reduce computation time.

Add this test to the `test/stake/LayerEdgeStakingTest.t.sol`.

```solidity
    function test_POC2() public {

        address staker;

        uint totalStakes = 200;

        for (uint i; i < totalStakes; i++) {
            staker = address(uint160(i + 1));
            vm.prank(admin);
            token.transfer(staker, 3000 ether);
            vm.startPrank(staker);
            token.approve(address(staking), 3000 ether);
            staking.stake(3000 ether);
            vm.stopPrank();
        }

        uint tier1Count;
        uint tier2Count;
        uint tier3Count;

        for (uint i; i < totalStakes; i++) {
            staker = address(uint160(i + 1));
            uint currentTier = uint(staking.getCurrentTier(staker));
            //console.log("USER INDEX: ", i + 1, "TIER: ", currentTier);
            if (currentTier == 1) {
                tier1Count++;
            } else if (currentTier == 2) {
                tier2Count++;
            } else if (currentTier == 3) {
                tier3Count++;
            }
        }

        console.log("Tier 1 Percentage: ", 100 * tier1Count / totalStakes);
        console.log("Tier 2 Percentage: ", 100 * tier2Count / totalStakes);
        console.log("Tier 3 Percentage: ", 100 * tier3Count / totalStakes);

    }
```

**OUTPUT**

```bash
[PASS] test_POC2() (gas: 66031634)
Logs:
  Tier 1 Percentage:  20
  Tier 2 Percentage:  43
  Tier 3 Percentage:  36
```

From the above output, Tier 2 has about 43%, which is wrong as Tier 2 is designed to hold only 43%.

### Mitigation

```diff

    function _stake(uint256 amount, address userAddr, bool isNative) internal {
...
-        if (!user.isActive && amount < minStakeAmount) {
+       if (!user.isActive && amount < minStakeAmount || stakerCountInTree + 1 >= MAX_USER) {
            stakerCountOutOfTree++;
            user.isActive = true;
            user.outOfTree = true;
            _recordTierChange(userAddr, tier);
        }

        // If first time staking, register staker position whose stake is more than minStakeAmount
-        if (!user.isActive && amount >= minStakeAmount) { 
+        if (!user.isActive && amount >= minStakeAmount && stakerCountInTree < MAX_USER) { 
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);
            _checkBoundariesAndRecord(false);
        }

        // Update user balances
        user.balance += amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked += amount;

        emit Staked(userAddr, amount, tier);
    }

```