Silly Sand Aphid

Medium

# The desired tier distribution does not hold

### Summary

The README states that 20% of stakers should be tier 1, 30% tier 2, and 50% tier 3 , and that any violation of this should automatically be considered a valid issue

However, if there are 9 stakers, then there will be 1 in tier 1, 2 in tier 2, and 6 in tier 3, for an 11%/22%/66% split, very far from the desired amount.

Maybe this is the intended behavior, but the straightforward reading of the README is that it's not and also should automatically be considered a Medium even though I'd otherwise call it a Low. Well, the Sherlock format dictates that, when you think there's any chance the judges might consider it a Medium or higher, you have to submit, so here it is.

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/README.md?plain=1#L47-L48

```markdown
### Q: What properties/invariants do you want to hold even if breaking them has a low/unknown impact?
1. Tier distribution correctness: Exactly 20% in Tier 1, 30% in Tier 2, and 50% in Tier 3 (with minimum guarantees of at least 1 user per tier when applicable)
```


### Root Cause

The error is in the rounding in this function:

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L480-L509

```solidity
function getTierCountForStakerCount(uint256 stakerCount)
        public
        pure
        returns (uint256 tier1Count, uint256 tier2Count, uint256 tier3Count)
    {
        // Calculate tier 1 count (20% of active stakers)
        tier1Count = (stakerCount * TIER1_PERCENTAGE) / 100;

        // Ensure at least 1 staker in tier 1 if there are any active stakers
        if (tier1Count == 0 && stakerCount > 0) {
            tier1Count = 1;
        }

        // Calculate remaining stakers after tier 1
        uint256 remainingAfterTier1 = stakerCount > tier1Count ? stakerCount - tier1Count : 0;

        // Calculate tier 2 count (30% of total active stakers, but don't exceed remaining)
        uint256 calculatedTier2Count = (stakerCount * TIER2_PERCENTAGE) / 100;

        if (calculatedTier2Count == 0 && remainingAfterTier1 > 0) {
            tier2Count = 1;
        } else {
            tier2Count = calculatedTier2Count > remainingAfterTier1 ? remainingAfterTier1 : calculatedTier2Count;
        }

        // Tier 3 is everyone else
        tier3Count = stakerCount > (tier1Count + tier2Count) ? stakerCount - tier1Count - tier2Count : 0;

        return (tier1Count, tier2Count, tier3Count);
    }
```

The problem is that both tier1Count and tier2Count get rounded down independently, leading them to have a combined much less than 50% of the total number of stakers.

Change the calculatedTier2Count to:

```solidity
    uint256 calculatedTier2Count = (stakerCount * TIER2_PERCENTAGE + (stakerCount*TIER1_PERCENTAGE - 100 * tier1Count)) / 100;
```

This ensures that tier1Count + tier2Count is as close to 50% as possible.

### Internal Pre-conditions

The discrepancy is highest when the number of stakers is congruent to 9 mod 10.

### External Pre-conditions

None

### Attack Path

N/A -- it's a bug, not an attack

### Impact

The distribution of tiers is wrong

Although this would ordinarily be a Low, the contest README says it should be valid.


https://github.com/sherlock-audit/2025-05-layeredge/blob/main/README.md?plain=1#L47-L48

```markdown
### Q: What properties/invariants do you want to hold even if breaking them has a low/unknown impact?
1. Tier distribution correctness: Exactly 20% in Tier 1, 30% in Tier 2, and 50% in Tier 3 (with minimum guarantees of at least 1 user per tier when applicable)
```


### PoC

_No response_

### Mitigation


Change the calculatedTier2Count to:

```solidity
    uint256 calculatedTier2Count = (stakerCount * TIER2_PERCENTAGE + (stakerCount*TIER1_PERCENTAGE - 100 * tier1Count)) / 100;
```

This ensures that tier1Count + tier2Count is as close to 50% as possible.