Big Cornflower Seal

High

# Incorrect tier update at _checkBoundariesAndRecord() will result incorrect interest calculation for stakers.

### Summary

At `_checkBoundariesAndRecord()`, it doesn't handle Tier 2 boundaries properly when it adds/removes new users to the tree.
Therefore, the Tier 2 users are not updated properly in `userTierHistory`.
So it will result incorrect interest calculation and users will get less/more tokens than they should get.

### Root Cause

When users stake/unstake tokens, it will update tiers of the users if the user is added/removed from the tree.
And it will update the tiers of the users in `_checkBoundariesAndRecord()`.

```solidity
function _stake(uint256 amount, address userAddr, bool isNative) internal {
   ...
   // If first time staking, register staker position whose stake is more than minStakeAmount
   if (!user.isActive && amount >= minStakeAmount) {
       ...
       _checkBoundariesAndRecord(false);
   }
   ...
}
```

At `_checkBoundariesAndRecord()`, when it updates tier 2, there is a logic mistake.
It compares `new_t2` and `old_t2` for tier 2 boundary handling. 
But `new_t1` and `old_t1` can be different while `new_t2` and `old_t2` are same.

So when a user is added to the tree, the tier of the user `new_t1 + new_t2` should be updated.
But it will call update of the `old_t1 + old_t2`.
So in this case, it doesn't call `_findAndRecordTierChange()` for the user whose tier should be updated.

`stakerTierHistory[]` is not updated for the user and it will result incorrect interest calculation.

```solidity
function _checkBoundariesAndRecord(bool isRemoval) internal {
    ...
    // Tier 2 boundary handling
    if (new_t1 + new_t2 > 0) {
        if (new_t2 != old_t2) { // @audit Here it only compares new_t2 and old_t2, but it should check new_t1 + new_t2 and old_t1 + old_t2
            // Need to update all users between the old and new tier 2 boundaries
            ....
        }
        // Handle case where Tier 2 count stays the same
        else if (isRemoval) {
            _findAndRecordTierChange(new_t1 + new_t2, n);
        } else if (!isRemoval) {   // @audit isRemoval is false, therefore it will update only old_t1 + old_t2
            _findAndRecordTierChange(old_t1 + old_t2, n);
        }
    }
   ...
}
```
### LoC
https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L876

### Internal Pre-conditions

N/A

### External Pre-conditions

N/A

### Attack Path

1. 14 users stake with valid amount ( `> minStakeAmount` )
Tier 1: 2
Tier 2: 4
Tier 3: 8
The tier level array for the users is [ 1, 1, 2, 2, 2, 2, 3, 3 .... 3 ]

2. 15th user stakes with valid amount
Tier 1: 3
Tier 2: 4
Tier 3: 8
The ideal tier level array for the users should be [ 1, 1, 1, 2, 2, 2, 2, 3, 3 .... 3 ]
But `old_t1 + old_t2` is 6, so it will update the 6th user only.
The 7th user remains tier 3, while it has to be tier 2.
The actual tier level array at this point is [ 1, 1, 1, 2, 2, 2, **_3_**, 3, 3 .... 3 ]

This is proved in PoC.

### Impact

Users will get less/more staking tokens than they should get from staking.

### PoC

```solidity
function test_TierSystem_For_15_stakers() public {
    vm.startPrank(admin);

    address[] memory testAddrs = new address[](15);
    for (uint i = 0; i < 15; i++) {
        string memory name = string(abi.encodePacked("user", vm.toString(i)));
        testAddrs[i] = makeAddr(name);
        token.transfer(testAddrs[i], 100_000 * 1e18);
    }

    vm.stopPrank();
    
    for (uint i = 0; i < 14; i++) {
        vm.startPrank(testAddrs[i]);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);
        vm.stopPrank();
    }
    
    // Check tier counts with 14 staker
    (uint256 tier1Count, uint256 tier2Count, uint256 tier3Count) = staking.getTierCounts();
    assertEq(tier1Count, 2);
    assertEq(tier2Count, 4);
    assertEq(tier3Count, 8);

    // Add one more staker

    vm.startPrank(testAddrs[14]);
    token.approve(address(staking), MIN_STAKE);
    staking.stake(MIN_STAKE);
    vm.stopPrank();


    (tier1Count, tier2Count, tier3Count) = staking.getTierCounts();
    assertEq(tier1Count, 3);
    assertEq(tier2Count, 4);
    assertEq(tier3Count, 8);

    // Check tier of 7th user
    (, LayerEdgeStaking.Tier expectedTier, , , LayerEdgeStaking.TierEvent[] memory tierHistory) = staking.getAllInfoOfUser(testAddrs[6]);
    
    uint256 lastTier = uint256(tierHistory[tierHistory.length - 1].to);

    assertEq(lastTier, uint256(expectedTier), "Last tier in tier history should be the same as the expected tier");
}
```

It results the following failure. 
```solidity
Failing tests:
Encountered 1 failing test in test/stake/Test.t.sol:LayerEdgeStakingTest
[FAIL: Last tier in tier history should be the same as the expected tier: 3 != 2] test_TierSystem_For_15_stakers() (gas: 6814148)
```

### Mitigation

It should update the logic for tier 2 boundary handling.
I recommend comparing `new_t1 + new_t2` and `old_t1 + old_t2`.

```solidity
function _checkBoundariesAndRecord(bool isRemoval) internal {
    ...
    // Tier 2 boundary handling
    if (new_t1 + new_t2 > 0) {
        if (new_t1 + new_t2 != old_t1 + old_t2) {
            // Need to update all users between the old and new tier 2 boundaries
            ....
        }
        ....
    }
   ...
}
```