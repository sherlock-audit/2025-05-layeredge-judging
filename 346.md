Muscular Plum Puma

Medium

# Some users can be unable to claim due to OOG error

### Summary

The stake(), unstake(), claimInterest() and compoundInterest() functions will all invoke _updateInterest(). The updateInterest() function will perform the following operations:
1) Read the user's tier history from storage:
```solidity
 // Get the user's tier history
        TierEvent[] storage userTierHistory = stakerTierHistory[userAddr];
```
2) Find the most recent tier event before the from time:
```solidity
 // Find the most recent tier event before fromTime
        for (uint256 i = 0; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp <= fromTime) {
                currentTier = userTierHistory[i].to;
                relevantStartIndex = i;
            } else {
                break;
            }
        }
```
In this step the function will loop through ALL the userTierHistory checkpoints until the most recent one is found.
3) Calculate the interest for the initial period:
4) Process all tiers until the current timestamp:
```solidity
        // Then process any subsequent tier changes within our calculation window
        for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp >= toTime) break;

            periodStart = userTierHistory[i].timestamp;
            periodEnd = (i == userTierHistory.length - 1) ? toTime : userTierHistory[i + 1].timestamp;
            if (periodEnd > toTime) periodEnd = toTime;

            if (periodEnd <= periodStart) continue;

            Tier periodTier = userTierHistory[i].to;
            uint256 apy = getTierAPYForPeriod(periodTier, periodStart, periodEnd);

            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
```
In this step, we can see that the ALL the other userTierHistory checkpoints will be iterated. Furthermore the getTierAPYForPeriod() also contains a loop which can be increased by the owner and loops through all the apy periods updated by the owner.


### Root Cause
Gas inefficient system design:
The root cause of this issue is that at some point, the _updateInterest() function will run out of gas. This can happen to users who stayed for a long time and got many updates added in their userTierHistory.
Furthermore, these checkpoints are not cleaned at all.

### Internal Pre-conditions

1. The user has to stay long enough for this issue to occur. 
2. There have to be many updates for the user's userTierHistory (from the _checkBoundariesAndRecord())
3. This can happen during very dynamic market conditions.
4. The owner may change the using the update TierAPY, which will further increase the iteration count.

### External Pre-conditions

N/A

### Attack Path

1. UserA has a position which stays for a long duration of time
2. Other users dynamically stake and unstake, which invokes the _checkBoundariesAndRecord, adding new checkpoints to userA's userTierHistory.
3. After many checkpoints have been added the user calls unstake() which reverts due to OOG error.

### Impact

Inability to withdraw funds from the contract. High gas costs.

### PoC

```solidity
function test_StakingTierBoundry_TierBoundaryChanges_WhenUsersUnstake() public {
        // Setup 10 users in the staking system
        vm.prank(alice);
        staking.stake(MIN_STAKE);

        vm.prank(bob);
        staking.stake(MIN_STAKE);

        vm.prank(charlie);
        staking.stake(MIN_STAKE);

        vm.prank(dave);
        staking.stake(MIN_STAKE);

        vm.prank(eve);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        LayerEdgeStaking.TierEvent[] memory eveHistory = getTierHistory(eve);
        assertEq(eveHistory.length, 1);
        assertEq(uint256(eveHistory[0].to), uint256(LayerEdgeStaking.Tier.Tier3));

        vm.prank(frank);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        eveHistory = getTierHistory(eve);

        vm.prank(grace);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        eveHistory = getTierHistory(eve);

        vm.prank(heidi);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        eveHistory = getTierHistory(eve);

        vm.prank(ivan);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        eveHistory = getTierHistory(eve);

        vm.prank(judy);
        staking.stake(MIN_STAKE);

        //Assert eve record history
        eveHistory = getTierHistory(eve);

        // Initial distribution should be:
        // Tier 1: Alice, Bob (first 2)
        // Tier 2: Charlie, Dave, Eve (next 3)
        // Tier 3: Frank, Grace, Heidi, Ivan, Judy (remaining 5)
        assertEq(uint256(staking.getCurrentTier(alice)), uint256(LayerEdgeStaking.Tier.Tier1));
        assertEq(uint256(staking.getCurrentTier(bob)), uint256(LayerEdgeStaking.Tier.Tier1));
        assertEq(uint256(staking.getCurrentTier(charlie)), uint256(LayerEdgeStaking.Tier.Tier2));
        assertEq(uint256(staking.getCurrentTier(eve)), uint256(LayerEdgeStaking.Tier.Tier2));
        assertEq(uint256(staking.getCurrentTier(frank)), uint256(LayerEdgeStaking.Tier.Tier3));

        // Fast forward past unstaking window
        vm.warp(block.timestamp + 7 days + 1);

        // Now have Bob unstake, this should push Charlie up to tier 1
        vm.prank(bob);
        staking.unstake(MIN_STAKE);
        //@audit adding the staking to simulate other users dynamic activity //time shall be ignored here as it may be several users.
        for (uint256 i = 0; i < 1000; i++) {
            address a = address(uint160(uint256(keccak256(abi.encodePacked(i)))));
            dealToken(address(a), LARGE_STAKE);
            vm.prank(address(a));
            staking.stake(MIN_STAKE);
            vm.warp(block.timestamp + 7 days + 1);
            vm.prank(address(a));
            staking.unstake(MIN_STAKE);
        }
        uint256 gas = gasleft();
        vm.prank(charlie);

        staking.stake(MIN_STAKE);
        gas -= gasleft();
        console2.log("Gas used:", gas);
        //Assert bob out of tree
        (,,,,,, bool outOfTree,,) = staking.users(bob);
        assertEq(outOfTree, true);

        //Assert eve record history

        eveHistory = getTierHistory(eve);

        // Verify tier changes
        assertEq(uint256(staking.getCurrentTier(alice)), uint256(LayerEdgeStaking.Tier.Tier1));
        assertEq(uint256(staking.getCurrentTier(charlie)), uint256(LayerEdgeStaking.Tier.Tier2)); // Charlie moved up to tier 1
        assertEq(uint256(staking.getCurrentTier(dave)), uint256(LayerEdgeStaking.Tier.Tier2));
        assertEq(uint256(staking.getCurrentTier(eve)), uint256(LayerEdgeStaking.Tier.Tier3));
        assertEq(uint256(staking.getCurrentTier(frank)), uint256(LayerEdgeStaking.Tier.Tier3)); // Frank moved up to tier 2
        assertEq(uint256(staking.getCurrentTier(grace)), uint256(LayerEdgeStaking.Tier.Tier3));

        // Bob should be unstaked and inactive
        (uint256 bobBalance,,,) = staking.getUserInfo(bob);
        assertEq(bobBalance, 0);

        // Check tier history for Eve
        // eveHistory = getTierHistory(eve);
        // // assertEq(eveHistory.length, 3, "Length should be 3");
        // console2.log("eveHistory[0].to", uint256(eveHistory[0].to));
        // console2.log("eveHistory[1].to", uint256(eveHistory[1].to));
        // assertEq(uint256(eveHistory[0].to), uint256(LayerEdgeStaking.Tier.Tier3)); // Initial tier
        // assertEq(uint256(eveHistory[1].to), uint256(LayerEdgeStaking.Tier.Tier2)); // Demoted tier
        // assertEq(uint256(eveHistory[2].to), uint256(LayerEdgeStaking.Tier.Tier3)); // Promoted tier
    }
```
You can use this test to explore gas limits in TierBoundaryAndInterestTest.t.sol.
Number of iterations below @audit comment can be changed to reflect a more dynamic use activity.

### Mitigation

Consider timely updating the user's interest and cleaning up the unnecessary userTierHistory.