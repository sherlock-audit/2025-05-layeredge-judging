Narrow Laurel Nightingale

High

# High Gas Fees May Discourage Unstaking for Users Near Tier Boundaries

### Summary

The staking mechanism categorizes users into three tiers based on a first-come, first-served system: Tier 1 for the first 20% of users to stake, Tier 2 for the next 30%, and Tier 3 for the remaining 50%. Users positioned near the boundaries between these tiers are particularly susceptible to frequent tier changes as staking and unstaking activities occur, causing shifts in the user distribution across tiers. Each tier change triggers an update that appends a new entry to the stakerTierHistory array, which tracks the user’s tier transitions over time. When a user initiates an unstaking transaction, the contract loops through the stakerTierHistory array to process these changes. This can lead to significantly increased gas costs, especially if the array becomes large due to frequent tier transitions.

These dynamics disproportionately affect users near tier thresholds, making them more likely to experience repeated tier changes. As a result, they may face much higher gas fees when unstaking, discouraging them from interacting with the system. In severe cases, this could create denial-of-service–like conditions, where users avoid unstaking or become effectively "trapped" due to the high cost of exit. This undermines the fairness and accessibility of the staking protocol, posing serious usability and economic challenges over time.

### Root Cause

When users perform staking or unstaking, the contract updates their interest by calling the calculateUnclaimedInterest function (see [LayerEdgeStaking.sol#L397](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L397)). This function calculates and sets the unclaimed interest earned by the user. During this process, calculateUnclaimedInterest loops over the userTierHistory array to account for interest accrued throughout the user’s tier transitions. Because the userTierHistory array can grow large due to frequent tier changes,especially for users near tier boundaries. this looping operation leads to significantly increased gas costs during unstaking.

Here's the snippet of the code
```solidity
        for (uint256 i = 0; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp <= fromTime) {
                currentTier = userTierHistory[i].to;
                relevantStartIndex = i;
            } else {
                break;
            }
        }

        // Then process any subsequent tier changes within our calculation window
        for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp >= toTime) break;

            periodStart = userTierHistory[i].timestamp;
            periodEnd = (i == userTierHistory.length - 1) ? toTime : userTierHistory[i + 1].timestamp;
            if (periodEnd > toTime) periodEnd = toTime;

            if (periodEnd <= periodStart) continue;

            Tier periodTier = userTierHistory[i].to;
            uint256 apy = getTierAPYForPeriod(periodTier, periodStart, periodEnd);

            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
```
High gas costs during unstaking stem from looping over the growing userTierHistory array, which records frequent tier changes—especially for users near tier thresholds. This array is updated in the contract at [LayerEdgeStaking.sol#L834](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L834).

```solidity
    function _recordTierChange(address user, Tier newTier) internal {
        // Get current tier
        Tier old = Tier.Tier3;

        if (stakerTierHistory[user].length > 0) {
            old = stakerTierHistory[user][stakerTierHistory[user].length - 1].to;
        }

        // If this is the same tier as before, no change to record
        if (
            stakerTierHistory[user].length > 0
                && stakerTierHistory[user][stakerTierHistory[user].length - 1].to == newTier
        ) return;

        uint256 currentTime = block.timestamp;

        //push event - ensure neither from nor to is Tier.None
        stakerTierHistory[user].push(TierEvent({from: old, to: newTier, timestamp: currentTime}));

        users[user].lastTimeTierChanged = currentTime;

        emit TierChanged(user, newTier);
    }
```

The root cause of the excessive gas costs during unstaking is the unbounded growth of the userTierHistory array, which records every tier change for all users. While tier updates occur for any user, those near tier thresholds are most likely to experience frequent tier switches due to their position. These switches result not only from their own staking and unstaking but also from similar actions by other users that shift the overall stake distribution. Each tier change appends a new entry to this array, causing it to grow continuously. When a user initiates unstaking, the contract must loop through the entire userTierHistory array, leading to very high gas fees. This unbounded looping over a growing history array, driven by dynamic stake behavior near tier boundaries, is the primary cause of the gas cost spike.

### Internal Pre-conditions

-

### External Pre-conditions

1. The total number of stakers tends to stabilize around a specific figure, for example, 1,000. Beyond this, any fluctuations in the staker count result solely from new participants joining through staking or existing participants leaving via unstaking. This behavior is highly likely to occur.

### Attack Path

There is no attack vector involved. this is normal system behavior. but it can negatively impact users who are near tier thresholds.

### Impact

Users near tier thresholds may lose access to their funds because excessively high gas costs discourage or prevent them from performing unstaking. Also, this issue is very likely to occur even without a deliberate attacker, effectively happening during normal contract usage. The number of affected users will increase significantly too if the inflow of new users staking and the outflow of users unstaking are both large and equal in size.


### PoC

add this function on the LayerEdgeStakingTest.t.sol

This scenario is a simulation based on the external pre-condition where a user is near a tier threshold, the total number of stakers has stabilized or converged, and then new users stake while existing users unstake, and so on.

```solidity
    function test_DOSVulnerability() public {
        address user;
        address newUser;
        address victim = address(0x4269);

        vm.pauseGasMetering();

        for(uint256 i = 1; i < 1001; i++) {
            if(i == 200) {
                user = victim;
            }
            else {
                user = address(uint160(i));
            }
            deal(address(token), user, 5000 ether);
            vm.startPrank(user);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
            vm.stopPrank();
        }


        uint256 timeStamp = block.timestamp;

        for(uint256 i = 201; i < 1000; i++) {
            skip(timeStamp + i * 10);
            user = address(uint160(i));
            newUser = address(uint160(i+1000));

            vm.prank(user);
            staking.unstake(MIN_STAKE);
            
            skip(timeStamp + i * 10 + 5);

            deal(address(token), newUser, 5000 ether);
            vm.startPrank(newUser);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
            vm.stopPrank();            
        }

        vm.resumeGasMetering();

        vm.txGasPrice(1);

        vm.startPrank(victim);

        uint256 gasStart = gasleft();
        staking.unstake(MIN_STAKE);
        uint256 gasEnd = gasleft();

        uint256 gasUsedForNearThreshold = (gasStart - gasEnd) * tx.gasprice;
        vm.stopPrank();

        console2.log("gas used for user near from threshold tier is %d", gasUsedForNearThreshold);

        vm.startPrank(address(0x1));

        gasStart = gasleft();
        staking.unstake(MIN_STAKE);
        gasEnd = gasleft();

        uint256 gasUsedForNotNearThreshold = (gasStart - gasEnd) * tx.gasprice;
        vm.stopPrank();

        console2.log("gas used for user far from threshold tier is %d", gasUsedForNotNearThreshold);

        console2.log("gas used for user near threshold tier is %d times higher than gas used for user far from threshold tier", gasUsedForNearThreshold/gasUsedForNotNearThreshold);
    }
```

add these two line in foundry.toml before run the test

```toml
[dev]
gas_limit = "18446744073709551615"
```

and then run `forge test --mt test_DOSVulnerability -vvv`

Here is the result
```bash
[⠊] Compiling...
No files changed, compilation skipped

Ran 1 test for test/stake/LayerEdgeStakingTest.t.sol:LayerEdgeStakingTest
[PASS] test_DOSVulnerability() (gas: 11570334)
Logs:
  gas used for user near from threshold tier is 11258106
  gas used for user far from threshold tier is 347411
  gas used for user near threshold tier is 32 times higher than gas used for user far from threshold tier

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 7.82s (7.80s CPU time)

Ran 1 test suite in 7.88s (7.82s CPU time): 1 tests passed, 0 failed, 0 skipped (1 total tests)
```

That a high increases of gas cost (32 times higher than usual) that can cause DOS for users near tier threshold when perform unstaking.

### Mitigation

Introduce a mapping-based structure to track the duration a user has spent in each tier. Instead of storing every tier transition in an unbounded array, maintain a mapping from tier to the total time the user has remained in that tier. This approach bounds the data structure, eliminates the need for looping over tier history during unstaking, and significantly reduces gas consumption.