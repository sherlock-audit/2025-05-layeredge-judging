Dazzling Blood Swan

Medium

# Sudden changes in minStakeAmount could leave user in Tier3 permanently

### Summary

User that stakes for the first time may be mistakenly put in Tier3 and out of the tree if  `minStakingAmount` is changed just before their stake.

### Root Cause

The system expects that users who stake for the first time less than `minStakingAmount` or leave their remaining stake when unstaking below that amount will belong to Tier3 permanently.
This can be a problem if a user wants to stake over the min amount but in that same block the `minStakingAmount` is being changed by owner: user could mistakenly stake less than the new min amount (not knowing that this amount is about to become bigger) and be set out of staking tree and in Tier3 permanently, with no possible option of fixing this.

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L674-L723

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L725-L755

### Internal Pre-conditions

1. Current minStakingAmount is 10000 tokens

### Attack Path

1. Alice wants to stake for the first time in the protocol so that she gets into staking tree. She calls `stake()` with amount = 10000 tokens, expecting to get into the tree.
2. Owner decides (due to external reasons) that the minimum staking amount should be 11000 tokens, so the calls setMinStakeAmount(12000).
3. Owner's transaction is executed before Alice's one, so that when Alice stakes the minStakingAmount is 11000 tokens; as Alice staked 10000 (less than the minimum) she will permanently stay out of tree and in Tier3. There is nothing Alice can do now to be promoted or get into the tree.

### Impact

Stakers could mistakenly be put out of the tree and in Tier3 permanently, with no option to improve their situation as once a user's stake drops below the minimum they will stay in the mentioned situation forever (even if the staker tries to stake many more tokens after this, there is no chance to upgrade).
The likelihood of this issue is low but feasible, as owner may need to change minStakeAmount in certain situations of the protocol and stakers will not be aware of this until the change has been made effective. The true issue is that once staker is out of tree, they cannot go back into it. Therefore, user must be conscious at any times that the operation he is doing will leave him out of the tree.

### Mitigation

When staking for the first time or unstaking, use a bool variable that tells whether staker wants to be in the tree:

```solidity
-  function _stake(uint256 amount, address userAddr, bool isNative) internal {
+ function _stake(uint256 amount, address userAddr, bool isNative, bool intoTree) internal {
        require(amount > 0, "Cannot stake zero amount");

        UserInfo storage user = users[userAddr];

        // Update interest before changing balance
        _updateInterest(userAddr);

        // Transfer tokens from user to contract
        if (!isNative) {
            require(stakingToken.transferFrom(userAddr, address(this), amount), "Token transfer failed");
        } else {
            IWETH(address(stakingToken)).deposit{value: amount}();
        }

        Tier tier = Tier.Tier3;

        //Staking for first time and amount is less than minStakeAmount, user will be in tier 3 permanently and out of the tree
        if (!user.isActive && amount < minStakeAmount) {
+         require(!intoTree, "Not enough stake to get into the tree");
            stakerCountOutOfTree++;
            user.isActive = true;
            user.outOfTree = true;
            _recordTierChange(userAddr, tier);
        }

        // If first time staking, register staker position whose stake is more than minStakeAmount
        if (!user.isActive && amount >= minStakeAmount) {
+         require(intoTree, "Stake over minimum amount, user would be set into the tree");
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);
            _checkBoundariesAndRecord(false);
        }
```
```solidity
- function _unstake(uint256 amount, address userAddr) internal {
+ function _unstake(uint256 amount, address userAddr, bool intoTree) internal {
        UserInfo storage user = users[userAddr];

        // require(user.isActive, "No active stake");
        require(user.balance >= amount, "Insufficient balance");
        // Update interest before changing balance
        _updateInterest(userAddr);

        // Update user balances
        user.balance -= amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked -= amount;

        if (!user.outOfTree && user.balance < minStakeAmount) {
            // execute this before removing from tree, this will make sure to calculate interest
            //for amount left after unstake
+          require(!intoTree, "Your stake is dropping below the minimum, you would go out of the tree");
            _recordTierChange(userAddr, Tier.Tier3);
            stakerTree.update(user.joinId, -1); //If one user goes out of tree, other users' rank lowers by 1 (rank improves!).
            stakerCountInTree--;
            user.outOfTree = true;
            stakerCountOutOfTree++;
            _checkBoundariesAndRecord(true);
        }

        // Add unstake request instead of immediate transfer
        unstakeRequests[userAddr].push(UnstakeRequest({amount: amount, timestamp: block.timestamp, completed: false}));

        emit UnstakedQueued(userAddr, unstakeRequests[userAddr].length - 1, amount);
    }
```