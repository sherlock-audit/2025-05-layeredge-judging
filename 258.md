Silly Sand Aphid

High

# Under certain circumstances, a staker may remain at tier 2, no matter how many people unstake

### Summary

The logic for updating a staker's tier is incorrect for the case where the number at tier 1 changes while the number at tier 2 remains the same. This occurs when the number of stakers changes from 14 to 15 or from 15 to 14; from 24 to 25 or from 25 to 24; etc. (This does not occur with fewer than 10 stakers, due to special cases in the logic.)

This can create a situation where there are exactly 7 stakers, where stakers number 4-6 are tier 3 as they should be, and staker number 7 is tier 2, and therefore receives too much interest.

### Root Cause

Consider the code that runs to update tiers when the number of tier 1 stakers changes but the number of tier 2 stakers does not.

https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L894-L898

These two cases are swapped. During removal, the user at the old boundary may change from tier 3 to tier 2, but it only checks the user at the new boundary.

When the number of stakers changes from 14 to 15, the staker ranked 7 should be upgraded to tier 2 but remains at tier 3. They will instead be upgraded to tier 2 when the 16th staker is added; even though adding a 16th, 26th, 36th etc staker can never change anyone's tier, the code will still check, partially cancelling out this bug.

HOWEVER, say then stakers 16 and 15 unstake. After staker 15 unstakes, the code should check the staker at rank 7, but it instead checks the staker at rank 6. After staker 14 unstakes, the code then checks the staker at rank 6 again.  Under many, and perhaps all, possible code paths, the rank 7 staker will never get demoted.

In general, stakers 7, 12, 17, 24, etc can all get lucky in this manner.

This flies under the radar because the code is similar to the logic for updating tier 1 stakers.

```solidity
            else if (isRemoval && new_t1 > 0) {
                _findAndRecordTierChange(new_t1, n);
            } else if (!isRemoval) {
                _findAndRecordTierChange(old_t1, n);
            }
```

But in this case, new_t1=old_t1, so the mixup is harmless. But the almost-identical code in the other case is very much not harmless.

### Internal Pre-conditions

Whenever a staker of rank 5 mod 10 (and > 10) is added, and then many stakers unstake, this occurs, with the beneficiary being one of rank 2 mod 5.

### External Pre-conditions

None

### Attack Path

Non-attack version:
* See the "Root Cause" and POC sections; this can just happen naturally

Attack version:

1. Attacker waits for the next staker rank available to be congruent to 2 mod 5, and at least 7
2. Attacker stakes, becoming staker ranked K
3. Attacker then stakes the minimum amount from K+2 other addresses. The address at rank K is promoted to tier 2. The attacker then unstakes from those other K+2 addresses, waits a week, and then deposits the money into the address at rank K.
4. The attacker, is now tier 2, even though the next many stakers will be tier 3. The attacker can thus swoop in arbitrarily late and get that juicy 35% interest.

### Impact

Some stakers will get more interest than they should.

### PoC

At the top of TierBoundaryInterestTest.t.sol

```solidity

    // Users
    address public admin = makeAddr("admin");
    address public alice = makeAddr("alice");
    address public bob = makeAddr("bob");
    address public charlie = makeAddr("charlie");
    address public dave = makeAddr("dave");
    address public eve = makeAddr("eve");
    address public frank = makeAddr("frank");
    address public grace = makeAddr("grace");
    address public heidi = makeAddr("heidi");
    address public ivan = makeAddr("ivan");
    address public judy = makeAddr("judy");
    address public kelly = makeAddr("kelly");
    address public liam = makeAddr("liam");
    address public mary = makeAddr("mary");
    address public noah = makeAddr("noah");
    address public olivia = makeAddr("olivia");
    address public peter = makeAddr("peter");
```

At the bottom:


```solidity
    function getActualTier(address user) public view returns (LayerEdgeStaking.Tier) {
        (,,,, LayerEdgeStaking.TierEvent[] memory history) = staking.getAllInfoOfUser(user);
        return history[history.length - 1].to;
    }


    function test_LayerEdgeStaking_15thStaker() public {

        // Setup initial 14 stakers
        address[14] memory initialStakers = [
            alice, bob, charlie, dave, eve, frank,
            grace, heidi, ivan, judy,
            kelly, liam, mary, noah
        ];
        
        // Setup initial stakers
        for (uint256 i = 0; i < initialStakers.length; i++) {
            dealToken(initialStakers[i], MIN_STAKE);
            vm.startPrank(initialStakers[i]);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
            vm.stopPrank();
        }

        // Add 15th staker
        dealToken(olivia, MIN_STAKE);
        vm.startPrank(olivia);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);
        vm.stopPrank();

        // At this point, Grace should be in Tier 2, but is actually in Tier 3. The getCurrentTier function lies about her tier.
        (,LayerEdgeStaking.Tier supposedTier,,, LayerEdgeStaking.TierEvent[] memory graceHistoryAfter) = staking.getAllInfoOfUser(grace);
        LayerEdgeStaking.TierEvent memory graceLastTierChangeEventAfter = graceHistoryAfter[graceHistoryAfter.length - 1];

        // This assertion will fail, which means Grace will not get enough interest.
        // But comment it out to see the rest of the test. It gets worse from here.
        assertEq(uint256(supposedTier), uint256(graceLastTierChangeEventAfter.to), "The tier returned by getCurrentTier should match the actual tier, as determined by their last tier change");

        // Now let's prove that Grace is getting 20% interest, not the 35% she's owed
        vm.warp(block.timestamp + 365 days);
        (,,, uint256 graceInterest) = staking.getUserInfo(grace);
        (,,, uint256 frankInterest) = staking.getUserInfo(frank);
        console2.log("Frank's interest after 1 year:", frankInterest);
        console2.log("Grace's interest after 1 year:", graceInterest);

        // Indeed, Grace is getting less interest than Frank, even though they should be both in Tier 2
        // This assertion will fail. Comment it out to proceed. It gets really bad from here.
        assertEq(frankInterest, graceInterest, "Frank and Grace should get the same interest");
        
        // Add 16th staker
        dealToken(peter, MIN_STAKE);
        vm.startPrank(peter);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);
        vm.stopPrank();


        address[9] memory toUnstake = [heidi, ivan, judy, kelly, liam, mary, noah, olivia, peter];
        for (uint256 i = 0; i < toUnstake.length; i++) {
            vm.startPrank(toUnstake[i]);
            staking.unstake(MIN_STAKE);
            vm.stopPrank();
        }


        // Print tiers for all stakers from alice to grace
        address[7] memory stakersToPrint = [alice, bob, charlie, dave, eve, frank, grace];
        string[7] memory names = ["Alice", "Bob", "Charlie", "Dave", "Eve", "Frank", "Grace"];
        
        console2.log("\nTier status for all stakers after unstaking:");
        for (uint i = 0; i < stakersToPrint.length; i++) {
            LayerEdgeStaking.Tier tier = getActualTier(stakersToPrint[i]);
            console2.log(string.concat(names[i], "'s tier:"), uint256(tier));
        }

        /**
         * Output:
         *
         * Tier status for all stakers after unstaking:
         * Alice's tier: 1
         * Bob's tier: 2
         * Charlie's tier: 2
         * Dave's tier: 3
         * Eve's tier: 3
         * Frank's tier: 3
         * Grace's tier: 2 <-- WTF???
         */

         // And if for one last failing assertion, let's just prove that Grace is in a lower-numbered tier than the earlier staker Frank, WHICH SHOULD NEVER HAPPEN
         assertEq(getActualTier(grace) >= getActualTier(frank), true, "Grace should be higher tier than Frank");
    }
```

### Mitigation

Swap the two swapped code branches. But really, make that function simpler. It's way too complicated -- no wonder there's a bug!