Old Admiral Badger

High

# An edge case in `_checkBoundariesAndRecord()` causes wrong tier tracking

### Summary

An edge case in `_checkBoundariesAndRecord()` causes wrong tier tracking

### Root Cause

Code behaves incorrectly when after a stake T1 increases but T2 stays the same

### Internal Pre-conditions

- after a user stakes, T1 count must increase but T2 count must stay the same (happens after just 15 stakes)

### External Pre-conditions

-

### Attack Path

1. There are 14 stakers which follows this distribution:
- 2 T1 stakers (14 * 0.2)
- 4 T2 stakers (14 * 0.3)
- 8 T3 stakers (remaining)
2. A new user stakes so distribution must be:
- 3 T1 stakers (15 * 0.2)
- 4 T2 stakers (15 * 0.3)
- 8 T3 stakers (remaining)
3. In `_checkBoundariesAndRecord()` we go here and put the 3rd rank into T1:
```solidity
        if (new_t1 != 0) {
            if (new_t1 != old_t1) {
                // Need to update all users between the old and new boundaries
                if (new_t1 > old_t1) {
                    // Promotion case: update all users from old_t1+1 to new_t1
                    for (uint256 rank = old_t1 + 1; rank <= new_t1; rank++) {
                        _findAndRecordTierChange(rank, n);
                    }
                } else {
                    // ...
                }
            }
            // ...
        }
```
4. Then in the T2 handling block, we go in the non-removal, old T2 == new T2 block:
```solidity
_findAndRecordTierChange(old_t1 + old_t2, n);
```
5. We write to 6th rank but we should be writing to 7th, so distribution is now (which is completely incorrect for T2 and T3 stakers):
- 3 T1 stakers
- 3 T2 stakers
- 9 T3 stakers

### Impact

Incorrect tier tracking, incorrect reward distribution, loss of funds for user due to lowered APY

### PoC
First, add this helper function in the staking contract to get the user's tier easily:
```solidity
    function getTier(address user) public view returns (uint256) { // note: added
        if (stakerTierHistory[user].length == 0) return 0;
        TierEvent memory te = stakerTierHistory[user][stakerTierHistory[user].length - 1];
        return uint256(te.to);
    }
```
Paste the following test into the staking contract test file:
```solidity
       function testEdgeCase() public {
        for (uint256 i; i < 14; i++) {
            address user = address(uint160(i + 1));
            deal(address(token), user, 3000e18);
            vm.startPrank(user);
            token.approve(address(staking), MIN_STAKE);
            staking.stake(MIN_STAKE);
        }

        uint256 t1Count;
        uint256 t2Count;
        uint256 t3Count;
        for (uint256 i; i < 14; i++) {
            address user = address(uint160(i + 1));
            uint256 tier = staking.getTier(user);
            if (tier == 1) {
                t1Count++;
                continue;
            }

            if (tier == 2) {
                t2Count++;
                continue;
            }

            if (tier == 3) {
                t3Count++;
                continue;
            }
        }

        assertEq(staking.stakerCountInTree(), 14);
        (uint256 t1ShouldBe, uint256 t2ShouldBe, uint256 t3ShouldBe) = staking.getTierCountForStakerCount(staking.stakerCountInTree());

        assertEq(t1Count, 2);
        assertEq(t1Count, t1ShouldBe);

        assertEq(t2Count, 4);
        assertEq(t2Count, t2ShouldBe);

        assertEq(t3Count, 8);
        assertEq(t3Count, t3ShouldBe);

        address newUser = makeAddr("new");
        deal(address(token), newUser, 3000e18);
        vm.startPrank(newUser);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);

        t1Count = 0;
        t2Count = 0;
        t3Count = 0;
        for (uint256 i; i < 15; i++) {
            address user = i == 14 ? newUser : address(uint160(i + 1));
            uint256 tier = staking.getTier(user);
            if (tier == 1) {
                t1Count++;
                continue;
            }

            if (tier == 2) {
                t2Count++;
                continue;
            }

            if (tier == 3) {
                t3Count++;
                continue;
            }
        }
        assertEq(staking.stakerCountInTree(), 15);
        (t1ShouldBe, t2ShouldBe, t3ShouldBe) = staking.getTierCountForStakerCount(staking.stakerCountInTree());

        assertEq(t1Count, 3);
        assertEq(t1ShouldBe, t1Count); // Correct.

        assertEq(t2Count, 3);
        assertNotEq(t2ShouldBe, t2Count); // WRONG!

        assertEq(t3Count, 9);
        assertNotEq(t3ShouldBe, t3Count); // WRONG!
    }
```

### Mitigation

Refactor the code to properly handle the case