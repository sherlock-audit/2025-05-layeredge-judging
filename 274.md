Joyful Tangelo Mustang

Medium

# Attacker can stake and unstake to increament the stakerTierHistory mapping and  force borderline user to OOG causing all their calls to stake/unstake/compound and claim interest to revert

### Summary

The decision to allow immediate unstaking after staking without delay will cause an out-of-gas (OOG) revert for borderline users as an attacker will rapidly stake and unstake to flood the stakerTierHistory mapping with changes. This results in excessive looping when affected users try to call stake(), unstake(), claimInterest(), or compound().

### Root Cause

The choice to remove the mandatory delay between staking and unstaking is a mistake as it allows an attacker to manipulate the stakerTierHistory mapping by repeatedly triggering tier changes, leading to bloated storage and unbounded loops in interest calculation (calculateUnclaimedInterest()).


```solidity

    function _stake(uint256 amount, address userAddr, bool isNative) internal {
        require(amount > 0, "Cannot stake zero amount");

        UserInfo storage user = users[userAddr];

        // Update interest before changing balance
        _updateInterest(userAddr);



    ..............



        // If first time staking, register staker position whose stake is more than minStakeAmount
        if (!user.isActive && amount >= minStakeAmount) {
            user.joinId = nextJoinId++;
            stakerTree.update(user.joinId, 1);
            stakerAddress[user.joinId] = userAddr;
            user.isActive = true;
            stakerCountInTree++;

            uint256 rank = stakerTree.query(user.joinId);
            tier = _computeTierByRank(rank, stakerCountInTree);
            user.isFirstDepositMoreThanMinStake = true;

            _recordTierChange(userAddr, tier);

@audit>>             _checkBoundariesAndRecord(false);
        }
```


```solidity


  function _unstake(uint256 amount, address userAddr) internal {
        UserInfo storage user = users[userAddr];

        // require(user.isActive, "No active stake");
        require(user.balance >= amount, "Insufficient balance");
        // Update interest before changing balance
        _updateInterest(userAddr);

        // Update user balances
        user.balance -= amount;
        user.lastClaimTime = block.timestamp;

        // Update total staked
        totalStaked -= amount;

        if (!user.outOfTree && user.balance < minStakeAmount) {
            // execute this before removing from tree, this will make sure to calculate interest
            //for amount left after unstake
            _recordTierChange(userAddr, Tier.Tier3);
            stakerTree.update(user.joinId, -1);
            stakerCountInTree--;
            user.outOfTree = true;
            stakerCountOutOfTree++;


@audit>>             _checkBoundariesAndRecord(true);
        }
```



```solidity


 /**
     * @notice Find the user at a given rank and record the tier change
     * @param rank The rank of the user
     * @param _stakerCountInTree The total number of stakers
     */
    function _findAndRecordTierChange(uint256 rank, uint256 _stakerCountInTree) internal {
        uint256 joinIdCross = stakerTree.findByCumulativeFrequency(rank);
        address userCross = stakerAddress[joinIdCross];
        uint256 _rank = stakerTree.query(joinIdCross);
        Tier toTier = _computeTierByRank(_rank, _stakerCountInTree);
        _recordTierChange(userCross, toTier);
    }
```



```solidity




    function _recordTierChange(address user, Tier newTier) internal {
        // Get current tier
        Tier old = Tier.Tier3;

        if (stakerTierHistory[user].length > 0) {
            old = stakerTierHistory[user][stakerTierHistory[user].length - 1].to;
        }

        // If this is the same tier as before, no change to record
        if (
            stakerTierHistory[user].length > 0
                && stakerTierHistory[user][stakerTierHistory[user].length - 1].to == newTier
        ) return;

        uint256 currentTime = block.timestamp;

        //push event - ensure neither from nor to is Tier.None

@audit>>        stakerTierHistory[user].push(TierEvent({from: old, to: newTier, timestamp: currentTime}));

        users[user].lastTimeTierChanged = currentTime;

        emit TierChanged(user, newTier);
    }
```



```solidity




  // Update interest before changing balance
@audit>>         _updateInterest(userAddr);


```


```solidity



  /**
     * @notice Update user's interest
     * @param userAddr User address
     */
    function _updateInterest(address userAddr) internal {

@audit>>         users[userAddr].interestEarned = calculateUnclaimedInterest(userAddr);
    }


```



```solidity


    /**
     * @notice Calculate unclaimed interest for a user
     * @param userAddr User address
     * @return totalInterest Total unclaimed interest
     */
    function calculateUnclaimedInterest(address userAddr) public view returns (uint256 totalInterest) {
        UserInfo memory user = users[userAddr];

        // Return stored interest if no balance
        if (user.balance == 0) return user.interestEarned;

        // Start with stored interest
        totalInterest = user.interestEarned;

        // Get the user's tier history
        TierEvent[] storage userTierHistory = stakerTierHistory[userAddr];

        if (userTierHistory.length == 0) return totalInterest;

@audit>>         uint256 fromTime = user.lastClaimTime;
        uint256 toTime = block.timestamp;

        // Find the tier the user was in at fromTime
        Tier currentTier = Tier.Tier3; // Default tier if no history
        uint256 relevantStartIndex = 0;

        // Find the most recent tier event before fromTime
@audit>>         for (uint256 i = 0; i < userTierHistory.length; i++) {
@audit>>             if (userTierHistory[i].timestamp <= fromTime) {
                currentTier = userTierHistory[i].to;
                relevantStartIndex = i;
            } else {
                break;
            }
        }

        // Process tier periods starting from the relevant tier
        uint256 periodStart = fromTime;
        uint256 periodEnd;

        // First handle the tier the user was in at fromTime
        if (
            relevantStartIndex + 1 < userTierHistory.length
                && userTierHistory[relevantStartIndex + 1].timestamp < toTime
        ) {
            periodEnd = userTierHistory[relevantStartIndex + 1].timestamp;
        } else {
            periodEnd = toTime;
        }

        // Calculate interest for this initial period
        if (periodEnd > periodStart) {
            uint256 apy = getTierAPYForPeriod(currentTier, periodStart, periodEnd);
            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }

        // Then process any subsequent tier changes within our calculation window
@audit>>         for (uint256 i = relevantStartIndex + 1; i < userTierHistory.length; i++) {
            if (userTierHistory[i].timestamp >= toTime) break;

            periodStart = userTierHistory[i].timestamp;
            periodEnd = (i == userTierHistory.length - 1) ? toTime : userTierHistory[i + 1].timestamp;
            if (periodEnd > toTime) periodEnd = toTime;

            if (periodEnd <= periodStart) continue;

            Tier periodTier = userTierHistory[i].to;
            uint256 apy = getTierAPYForPeriod(periodTier, periodStart, periodEnd);

            uint256 periodInterest =
                ((user.balance * apy * (periodEnd - periodStart)) / (SECONDS_IN_YEAR * PRECISION)) / 100;
            totalInterest += periodInterest;
        }
        return totalInterest;
    }
```


### Internal Pre-conditions

1. Attacker has large funds to repeatedly stake and unstake with multiple accounts.

2. Users are at the boundary of Tier 1, 2 and 3, making them susceptible to boundary tier adjustments.

3. The users[userAddr].lastClaimTime is sufficiently in the past to make looping through stakerTierHistory expensive.


https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L748



https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L712


https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L834


https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L411-L465

### External Pre-conditions

1. Gas limit required to process the bloated stakerTierHistory loop.

2. The Ethereum base gas fee.

### Attack Path

1. Attacker rapidly calls stake() and unstake() with minStakeAmount multiple times with different address.

2. Each iteration triggers a tier change for users near the staking threshold by altering stakerCountInTree.

3. This causes the _checkBoundariesAndRecord() function to invoke _recordTierChange() on borderline users, updating their stakerTierHistory mapping.

Over time, stakerTierHistory for affected users grows significantly.

When the victim calls stake(), unstake(), compound(), or claimInterest(), their transaction reverts due to out-of-gas while looping through stakerTierHistory in calculateUnclaimedInterest().

This can also occur even during normal back and forth interactions of normal users.



### Impact

The affected users cannot execute any stake-related operations, including staking, unstaking, claiming interest or compounding. This results in a complete denial of service for those users. The attacker does not lose funds as he can leave some of his tokens at tier 3 to earn interest, but successfully griefs users into reverts by exploiting gas limits. This can also occur with normal users' activity

### PoC

_No response_

### Mitigation

1. Reintroduce a minimum unstaking delay (e.g., 7 days) after a stake to prevent immediate tier manipulation. Alternatively,  defer _checkBoundariesAndRecord(true) until after the unstake request period matures after the 7-day wait.
2. Alternatively, create a sync() function for users to sync their interests at intervals preventing the mapping from overgrowing extensively to trigger OOG.
3. Alternatively, Consider updating the user's interest and the boundary users last claim time also  in the call to promote or demote their tier.