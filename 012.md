Zealous Mint Bee

Medium

# Changing APY percentages will gradually increase gas cost for users to stake/unstake their tokens

### Summary

Changing APY percentages overtime will cause the gas cost of the ```calculateUnclaimedInterest``` function to grow. The ```calculateUnclaimedInterest``` function is used in the ```_updateInterest``` function - which is then used in the stake/unstake functionality of the contract. As the gas cost grows, it will become more expensive for users to stake/unstake their tokens and will eventually hit the block gas limit which will then cause the staking/unstaking functionality to be broken.

### Root Cause

The functions responsible for storing APY rate history are defined In LayerEdgeStaking:272 and LayerEdgeStaking:248 . The ```tierAPYHistory``` array (https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L261 https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L281-L283 is unbounded and grow overtime as the owner makes a change to the APY percentages.

This leads to the calculation of weighted average of the APY percentage for a said period to become more expensive because getTierAPYForPeriod ([LayerEdgeStaking.sol:536](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L536) relies on looping through the ```tierAPYHistory``` array ([LayerEdgeStaking.sol:560](https://github.com/sherlock-audit/2025-05-layeredge/blob/main/edgen-staking/src/stake/LayerEdgeStaking.sol#L560)) This calculation becoming more gas expensive leads to the function ```calculateUnclaimedInterest``` (used in ```_updateInterest```) to also become more gas expensive thus making the ```stake```/```unstake``` methods which use ```_updateInterest``` more expensive and prone to running out of gas, making the staking/unstaking functionality for users which have been active long enough for the calculation of the APY rates based on history relevant to them to exceed the gas limit.

### Internal Pre-conditions

-

### External Pre-conditions

-

### Attack Path

1. User stakes coins monthly
2. Owner changes APY percentages daily

### Impact

User pays more in gas fees or eventually hits the gas limit because of the ```for``` loop in the calculation for their rewards.

Example:
User stakes minimum staking amount every 30 days. 
Owner changes APY percentages daily.

Assume 600 days pass from the initial stake. The gas cost (assume 4gwei):

Day 30 (APY % have been changed 30 times - once daily)
GasUsed:  183053
Cost (ETH): 0.000732212000000000 ETH

Day 600 (APY % have been changed 600 times - once daily) 
GasUsed:  1006204
Cost (ETH): 0.004024816000000000 ETH

User pays 449% more gas in order to stake their tokens for the 21st month compared to their previous stakings. If APY percentages are changed more frequently or user has been staking their tokens for an extended period of time it's possible for them to hit the gas limit and won't be able to further stake or unstake their tokens.

### PoC

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.20;

import {Test} from "forge-std/Test.sol";
import {console2} from "forge-std/console2.sol";
import {LayerEdgeStaking} from "@src/stake/LayerEdgeStaking.sol";
import {LayerEdgeToken} from "@test/mock/LayerEdgeToken.sol";
import {IERC20} from "openzeppelin-contracts/contracts/token/ERC20/IERC20.sol";
import {DeployLayerEdgeStaking} from "@script/DeployLayerEdgeStaking.s.sol";
import {NetworkConfig, HelperConfig} from "@script/HelperConfig.s.sol";
import {OwnableUpgradeable} from "@openzeppelin/contracts-upgradeable/access/OwnableUpgradeable.sol";
import {PausableUpgradeable} from "@openzeppelin/contracts-upgradeable/utils/PausableUpgradeable.sol";

contract GasOverTimePOC is Test {
    LayerEdgeStaking public staking;
    LayerEdgeToken public token;
    HelperConfig public helperConfig;
    DeployLayerEdgeStaking public deployer;

    // Test users
    address public admin;
    address public alice = makeAddr("alice");
    
    // Constants for testing
    uint256 public constant MIN_STAKE = 3000 * 1e18; // Minimum stake amount
    uint256 public constant PRECISION = 1e18;

    function setUp() public {
        deployer = new DeployLayerEdgeStaking();
        (staking, helperConfig) = deployer.run();

        NetworkConfig memory config = helperConfig.getActiveNetworkConfig();

        token = LayerEdgeToken(config.stakingToken);
        admin = config.owner;
        vm.startPrank(admin);

        // Distribute tokens to users
        uint256 userAmount = 100_000 * 1e18; // 100k tokens per test user
        token.transfer(alice, userAmount);

        // Fund staking contract with reward tokens
        uint256 rewardAmount = 100_000 * 1e18; // 100k tokens for rewards
        token.approve(address(staking), rewardAmount);
        staking.depositRewards(rewardAmount);
        staking.setCompoundingStatus(true);
        staking.setMinStakeAmount(MIN_STAKE);
        vm.stopPrank();
    }

    function testGasOverTime() public {
        uint256 gasPrice = 4 gwei; // assume 4 gwei (taken average on Ethereum mainnet)
        uint256 tier1APY = 10 * PRECISION;
        uint256 tier2APY = 20 * PRECISION;
        uint256 tier3APY = 30 * PRECISION;

        uint256 totalDays = 600; // run for 600 days
        uint256 intervalDays = 30; // log every 30 days

        // inital alice stake
        vm.startPrank(alice);
        token.approve(address(staking), MIN_STAKE);
        staking.stake(MIN_STAKE);
        vm.stopPrank();

        // we’ll act as admin for updateAllAPYs
        vm.startPrank(admin);

        // freeze gas counting until our first log point
        vm.pauseGasMetering();

        uint256 gasFirst;
        uint256 gasLast;

        for (uint256 day = 1; day <= totalDays; day++) {
            // advance one day
            vm.warp(block.timestamp + 1 days);

            // bump APYs each day
            staking.updateAllAPYs(
                tier1APY + (day - 1),
                tier2APY + (day - 1),
                tier3APY + (day - 1)
            );

            // every intervalDays days, measure & log
            if (day % intervalDays == 0) {
                // start measuring
                vm.resumeGasMetering();
                uint256 g0 = gasleft();

                // do the action to measure
                vm.stopPrank();
                vm.startPrank(alice);
                token.approve(address(staking), MIN_STAKE);
                staking.stake(MIN_STAKE);
                vm.stopPrank();
                vm.startPrank(admin);

                // compute usage & cost
                uint256 used = g0 - gasleft();
                uint256 costWei = used * gasPrice;

                // record first vs last
                if (gasFirst == 0) {
                    gasFirst = used;
                }
                gasLast = used;

                // logs:
                console2.log("Day (APY changed times)", day);
                console2.log("GasUsed: ", used);
                emit log_named_decimal_uint("Cost (ETH)", costWei, 18);

                // freeze metering again until next interval
                vm.pauseGasMetering();
            }
        }

        // ——— Assertions & percent change ———
        // first stake gas must be lower than last stake gas
        assertLt(
            gasFirst,
            gasLast,
            "First interval gas should be lower than last"
        );

        // compute percent increase: ((last - first) * 100) / first
        uint256 pctIncrease = ((gasLast - gasFirst) * 100) / gasFirst;
        console2.log(pctIncrease, "% increase in gas usage over time");
        vm.stopPrank();
    }
}
```

### Mitigation

_No response_